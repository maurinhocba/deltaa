 SUBROUTINE masm15(ndime,nnode, heade,emass,mass,sumat,lumpd,nrf,rhead,ifpre)
 IMPLICIT NONE
 LOGICAL, INTENT(IN) :: lumpd
 INTEGER (kind=4), PARAMETER :: nnr = 2
 INTEGER (kind=4), INTENT(IN) :: ndime,nnode,nrf,ifpre(:,:)
 REAL (kind=8), INTENT(IN OUT) :: emass(:,:),sumat,mass(:)
 TYPE( ele15 ), POINTER :: heade
 TYPE( reinf ), POINTER :: rhead

 INTEGER (kind=4) :: inode,lnode,idofn,lm(3,3),isec,osec
 REAL (kind=8) :: diag,outd,suma,voli,rhoel,ms(45),thick,sume1
 TYPE( ele15 ), POINTER :: e
 TYPE( reinf ), POINTER :: rf
 TYPE (section), POINTER :: sec
 LOGICAL :: found

 IF(.NOT.lumpd)ms = 0d0
 sume1=0.
 osec = -1
 e => heade
 DO
   IF( .NOT.ASSOCIATED (e) )EXIT
   suma=0.
   isec = e%matno                    !associated material
   IF( isec /= osec )THEN
     CALL sect_search(isec,found,sec)
     IF( sec%secty == 12 )THEN
       thick = sec%rprop(1)                !shell thickness
       rhoel = sec%mtbas%prope(5)*thick    !surface density
     ELSE
       rhoel = sec%rprop(5)                !surface density
     END IF
     osec = isec
   END IF
   voli = e%area1*rhoel
   suma = suma + voli
   diag = voli/3d0

   !---generates lumped mass matrix proportional to diagonal

   DO inode=1,nnode
     lnode = e%lnods(inode)
     DO idofn=1,ndime
       emass(idofn,lnode)=emass(idofn,lnode)+diag
     END DO
   END DO
   sume1 = sume1 + suma
   IF(.NOT.lumpd)THEN  !consistent mass matrix
     diag = diag/2d0
     outd = diag/2d0
     !diagonal terms
     ms( 1) = diag
     ms(10) = diag
     ms(18) = diag
     ms(25) = diag
     ms(31) = diag
     ms(36) = diag
     ms(40) = diag
     ms(43) = diag
     ms(45) = diag
     !out diagonal terms
     ms( 4) = outd
     ms( 7) = outd
     ms(13) = outd
     ms(16) = outd
     ms(21) = outd
     ms(24) = outd
     ms(28) = outd
     ms(34) = outd
     ms(39) = outd
     lm(1:3,1:3) = ifpre(1:3,e%lnods(1:3))
     CALL ensmat(9,lm(1,1),ms(1),mass(1))
   END IF
   e => e%next
 END DO
 IF( nrf > 0 )THEN
   IF(.NOT.lumpd)ms(1:21) = 0d0
   osec = -1
   rf => rhead
   DO
     IF( .NOT.ASSOCIATED (rf) )EXIT
     suma=0.
     isec = rf%secno                    !associated material
     IF( isec /= osec )THEN
       CALL sect_search(isec,found,sec)
       rhoel = sec%rprop(1)  !linear density
       osec = isec
     END IF
     voli = rf%l0*rhoel
     suma = suma + voli
     diag = voli/2d0

     !---generates lumped mass matrix proportional to diagonal

     DO inode=1,nnr
       lnode = rf%lnods(inode)
       DO idofn=1,ndime
         emass(idofn,lnode)=emass(idofn,lnode)+diag
       END DO
     END DO
     sume1 = sume1 + suma
     IF(.NOT.lumpd)THEN  !consistent mass matrix
       diag = voli/3d0
       outd = diag/2d0
       !diagonal terms
       ms( 1) = diag
       ms( 7) = diag
       ms(12) = diag
       ms(16) = diag
       ms(19) = diag
       ms(21) = diag
       !out diagonal terms
       ms( 4) = outd
       ms(10) = outd
       ms(15) = outd
       lm(1:3,1:2) = ifpre(1:3,rf%lnods(1:2))
       CALL ensmat(6,lm(1,1),ms(1),mass(1))
     END IF
     rf => rf%next
   END DO
 END IF
 sumat = sumat + sume1
 !IF(iwrit == 1) &
 WRITE(lures,"(//'  total mass for shell element :', e15.7)")sume1
 RETURN
 END SUBROUTINE masm15
