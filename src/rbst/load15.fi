 SUBROUTINE load15 (igrav,force,gvect,gravy,heade,rhead)
 !********************************************************************
 !
 !***  evaluates gravity load vector for shell elements RBST
 !
 !********************************************************************
 IMPLICIT NONE

 INTEGER (kind=4), PARAMETER :: nnode=3, nn=2
 INTEGER (kind=4) :: igrav
 REAL (kind=8) :: force(:,:),gvect(3),gravy
 TYPE (ele15), POINTER  :: heade
 TYPE (reinf), POINTER  :: rhead

 !        local variables
 INTEGER (kind=4) n,l,isec,osec
 REAL (kind=8) weigt,fvect(3),rhoel
 TYPE (ele15), POINTER :: elem
 TYPE (reinf), POINTER :: rf
 TYPE (section), POINTER  :: sec
 LOGICAL :: found

 IF(igrav /= 0) THEN       !if gravity is present

   osec = -1
   elem => heade           !point to first element
   DO
     IF (.NOT.ASSOCIATED (elem) ) EXIT    !all elements processed, Exit loop

     isec  = elem%matno                     !associated material
     IF( isec /= osec )THEN
       CALL sect_search(isec,found,sec)
       IF( sec%secty == 12 )THEN
         rhoel = sec%mtbas%prope(5)*sec%rprop(1)    !surface density
       ELSE
         rhoel = sec%rprop(5)                !surface density
       END IF
       weigt = gravy*rhoel                   !surface density
       osec = isec
     END IF
     IF(weigt /= 0.0d0) THEN                !if not zero

       fvect = elem%area1*weigt*gvect/3d0   !force vector at each node

       DO n=1,nnode                          !for each node (nnode=3)
         l = elem%lnods(n)                   !node number
         force(1:3,l) = force(1:3,l) + fvect !sum on global force vector
       END DO
     END IF

     elem => elem%next                       !point to next element
   END DO
   ! reinforcements
   osec = -1
   rf => rhead           !point to first side
   DO
     IF (.NOT.ASSOCIATED (rf) ) EXIT    !all sides processed, Exit loop

     isec  = rf%secno                     !associated material
     IF( isec /= osec )THEN
       CALL sect_search(isec,found,sec)
       rhoel = sec%rprop(1)*sec%rprop(15)/sec%rprop(16) !linear density
       weigt = gravy*rhoel                        !linear force
       osec = isec
     END IF
     IF(weigt /= 0.0d0) THEN                !if not zero

       fvect = rf%l0*weigt*gvect/2d0        !force vector at each node

       DO n=1,nn                             !for each node (nn=2)
         l = rf%lnods(n)                     !node number
         force(1:3,l) = force(1:3,l) + fvect !sum on global force vector
       END DO
     END IF

     rf => rf%next                       !point to next rfent
   END DO
 END IF

 RETURN
 END SUBROUTINE load15
