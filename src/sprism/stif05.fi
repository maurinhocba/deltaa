 SUBROUTINE stif05(heade, gstif, force, coora, small, ngaus, quad, nnode)

 ! 6-node solid-shell element (Sprism).
 ! use Green-Lagrange strains and 2nd Piola-Kirchhoff stresses

 USE ele18_db, ONLY : rubber3d
 IMPLICIT NONE
 ! dummy arguments
 INTEGER (kind=4), INTENT(IN) :: ngaus, & !number of integration points
                                 nnode    !number of nodes in the patch
 LOGICAL, INTENT(IN) :: small,  & ! use Green-Lagrange strains
                        quad      ! use quadratic approac for in-plane metric tensor
 REAL (kind=8), INTENT(IN) :: coora(:,:)     ! present coordinates
 REAL (kind=8), INTENT(IN OUT) :: gstif(:),force(:)  !global stiffness and force vector
 TYPE (ele05), POINTER :: heade   !pointer to first element

 !Local Variables
 INTEGER (kind=4), PARAMETER :: ndime =3
 LOGICAL, PARAMETER :: naatst = .FALSE.    ! do not use large strains but for rubbers

 TYPE (ele05), POINTER :: e               !pointer to an element
 LOGICAL  :: newmt,   &  !if present material different than previous
             plast,   &  !if plastic strains may be present
             elast,   &  !if the material is elastic
             sides(3)    !if side element exists (quad approach)
 INTEGER (kind=4) :: i,j,g,k,ii,jj,   &  !different indexes
                     isec,osec,       &  !present and old section
                     mtype,           &  !material type
                     nlayr,           &  !number of material components
                     nvarl               !number of DOFs in the patch

 REAL (kind=8) :: propi(13),       & !(nnn) material properties
                  young,poiss,     & !mechanical properties
                  deatht,          & !death time for plasticity (not used)
                  d(6,6),          & !full constitutive matrix (constant)
                  cm(6,6),         & !full constitutive matrix (pull-back)
                  daux(21),        & !upper diagonal constitutive matrix x dvol
                  chi(12)            !(12) Hill coefficients

 REAL (kind=8) efpst,             & !effective plastic strain
               f3(3),c33,b3(3,6), & !gradient 3, C33 and associate B matrix
               stres(6),          & !2nd P-K stresses for Geometric matrix
               stran(6),          & !C tensor to pull-back constitutive matrix
               voli,              & !Gauss point volume
               lambd,             & !iterative consistency parameter
               fac                  !exp(2 x alpha x zeta)

 REAL (kind=8) :: bmatx(ndime,nnode,nstre), &   !global B matrix (to be interpolated)
                  x(ndime,nnode),           &   !local coordinates
                  cq(3,2)                       !in-plane metric tensor at faces
 REAL (kind=8), ALLOCATABLE ::  bq(:,:,:,:), &  !(ndime,nnb,3,2)  !in-plane B matrix at faces
                                tq(:,:,:,:), &  !(ndime,2,3,2)
                                stiff(:)        !stiffness matrix
 !           variables for interpolation into the Gauss points
 REAL (kind=8) :: thf(ngaus),   & !G-P positions
                  wei(ngaus),   & !G-P weigths
                  sh(3,ngaus),  & !Quadratic approach functions for Jacobian
                  shape(2,ngaus)  !Linear approach for C components
 ! variables for assumed strain transverse shear
 REAL (kind=8) :: tas(3,3),      & ! der x/x_3 at Assumed Strain points
                  gas(3,3),      & ! der x/tan at Assumed Strain points
                  vas(3,2),      & ! aux and C23,C13 at faces
                  bs(3,6,2,2)      ! Bs matrix at faces


 TYPE (section), POINTER :: sec   !pointer to section
 TYPE (mater), POINTER :: mat     !pointer to material
 !TYPE (mater), POINTER :: matm,matf !pointer to a material data

 INTERFACE
   INCLUDE 'modp18.h'
 END INTERFACE

 nvarl = ndime*nnode  !nnode*ndime number of element DOFs
 ALLOCATE (stiff(nvarl*(nvarl+1)/2))    !get memory for element stiffness matrix (upper part)
 IF( quad )THEN  !get memory for gradients at faces and associated B matrices
    ALLOCATE (  tq(ndime,2,3,2), bq(ndime,6,3,2) )       !quad approach
 ELSE
    ALLOCATE (  tq(ndime,2,1,2), bq(ndime,3,3,2) )       !standard approach
 END IF

 CALL gaussq(ngaus,thf(1),wei(1)) !integration points through the thickness
 wei = wei/2d0
 DO g=1,ngaus
   ! compute sh(3,ngaus) to evaluate jacobian
   sh(1,g) = -0.5d0*(1d0-thf(g))*thf(g)
   sh(2,g) =  1d0-thf(g)**2
   sh(3,g) =  0.5d0*(1d0+thf(g))*thf(g)
   ! compute shape(2,ngaus) to interpolate from upper and lower faces
   shape(1,g) =  0.5d0*(1d0-thf(g))
   shape(2,g) =  0.5d0*(1d0+thf(g))
 END DO

 !***  Compute Element Stiffness matrix for all elements
 bmatx = 0d0           !initializes B-matrix
 osec = -1             !initialize to a non-existent section
 e => heade            !point to first element

 DO
   IF (.NOT.ASSOCIATED (e) ) EXIT    !last element processed => EXIT
   isec = e%matno                    !associated section to element
   newmt = osec /= isec              !a different material must be considered
   IF(newmt)THEN                     !if new material, get properties
     osec = isec                     !keep present material
     sec => psecs(isec)%p            !point to section
     nlayr = sec%iprop(1)             !number of layers
     !nvar  = sec%iprop(2)             !total number of internal variables

     !************************** STANDARD SOLID *****************************************
     IF( nlayr == 0 )THEN            !standard solid section (not layered)

       mat => sec%mtbas                !point to associated material
       mtype = mat%mtype               !type of base material
       elast = mat%matdef(3) == 1      !elastic material
       !is    = mat%matdef(4)           !isotropic hardening model
       ! ------------------------------------------------------------------------------------
       ! ONLY MTYPES 1, 5, 6, 7 and 92 ARE ALLOWED
       SELECT CASE (mtype)
       ! ------------------------------------------------------------------------------------
       CASE (1)                        !Isotropic Elastic - Anisotropic Plastic  ( mtype == 1)
         !IF( .NOT.elast ) THEN
         !  ! e1, nu1, uniaxial, efren, consn, r, exponent m, hill 79
         !  propi(1:4) = mat%propp(1:4)       ! isotropic hardening parameters
         !  propi(5) = REAL( mat%matdef(4),8) ! isotropic hardening model
         !  chi    = mat%propp(16:27)         ! hill coefficients
         !  IF( mat%matdef(6) == 1  ) chi(1:6) = chi(7:12)  !associative
         !  deatht = mat%propp(5)             !end of plasticity
         !  plast = propi(1) > 0  !consider plasticity ?
           young = mat%prope(1)          ! young modulus
           poiss = mat%prope(2)          ! poisson ratio
         !  ! von Mises plasticity IF( mat%matdef(3) <= 2 )
           CALL modp18(d,young,poiss,propi,0d0,0d0,.FALSE.,elast,newmt)
         !ELSE
           plast = .FALSE.
        ! END IF
         ! ------------------------------------------------------------------------------------
       CASE (5)                        !Orthotropic Elastic - No Plasticity      ( mtype == 5)
         d = RESHAPE( mat%prope(26:61), (/6,6/) )
         elast = .TRUE.
         plast = .FALSE.                        !consider plasticity ?
       ! ------------------------------------------------------------------------------------
       CASE (6)                        !Hyperelastic isotropic rubber            ( mtype == 6)
         propi(1:12) = mat%prope(7:18)     !elastic properties
         elast = .TRUE.                    !elastic only
         plast = .FALSE.                   !consider plasticity ?
       ! ------------------------------------------------------------------------------------
       CASE (7)                        !Isotropic damague model                  ( mtype == 7)
         d = RESHAPE( mat%prope(45:80), (/6,6/) )                 !linear elastic integrated matrix
       !
       ! ------------------------------------------------------------------------------------
       CASE (92)                       !Simplified composite model              ( mtype == 92)
                                       !Fiber reinforced matrix (Vanishing fiber diameter model)
         plast = .NOT.elast                     !consider plasticity ?
         d = RESHAPE( mat%props(5:40), (/6,6/) )                 !linear elastic integrated matrix
         !point to apropriate material layer
         ! matm => pmats(mat%matdef(4))%p  !matm
         ! matf => pmats(mat%matdef(5))%p  !matf
       ! ------------------------------------------------------------------------------------
       END SELECT

       !************************** LAYERED SOLID *************************************************
     ELSE  !nlayr >= 1      layered solid (at integration point level)
       !thick = sec%rprop(1)             !original thickness
       !minstr= sec%rprop(4)             !strain threshold to use TTTI

       !nl=1
       elast = .TRUE. !sec%iprop(3) == 0               !elastic problem
       plast = .NOT.elast                      !consider plasticity ?
       d = RESHAPE( sec%rprop(6:41), (/6,6/) )                 !linear elastic integrated matrix
       !IF( plast )THEN  !plastic lamina
       !  IF( ALLOCATED ( rr ) )DEALLOCATE( thickl,zposl,rr,matsl,lvari )
       !  ALLOCATE( thickl(nlayr),zposl(nlayr),rr(5,nlayr),matsl(3,nlayr),lvari(nlayr) )
       !  i = 42                                !pointer to layer thickness
       !  CALL vecasi(nlayr,sec%rprop(i),thickl(1))        !layer thickness
       !  i = i+2*nlayr                         !pointer to layer z position
       !  !CALL vecasi(nlayr,sec%rprop(i),zposl(1))         !layer z position
       !  i = i+nlayr                           !pointer to layer rotation matrix
       !  CALL vecasi(nlayr*5,sec%rprop(i),rr(1,1))        !layer rotation matrix
       !
       !  lvari(1:nlayr)= sec%iprop(nlayr+5:2*nlayr+4)
       !  DO il=1,nlayr
       !    mate= sec%iprop(4+il)
       !    mat => pmats(mate)%p        !search layer material
       !    matsl(1,il)%p => mat
       !    SELECT CASE (mat%mtype)
       !    CASE ( 91,92 )
       !      !point to component materials data bases
       !      matsl(2,il)%p => pmats(mat%matdef(4))%p  !matm
       !      matsl(3,il)%p => pmats(mat%matdef(5))%p  !matf
       !    CASE DEFAULT
       !      NULLIFY( matsl(2,il)%p )
       !      NULLIFY( matsl(3,il)%p )
       !    END SELECT
       !  END DO
       !END IF
       !osec = -1                              !recompute constants
     END IF
     newmt = .FALSE.               !same material than previous ?

   END IF !newmt

   x(:,1:nnb) = coora(:,e%lnods(1:nnb))     !nodes of the element
   IF( quad )THEN            !for quad approach get extra-nodes coordinates
     DO i=1,3
       sides(i) = e%lnods(i+6) /= 0
       IF( sides(i) )THEN   !if side element exists
         x(:,i+6:i+9:3) = coora(:,e%lnods(i+6:i+9:3))  !get extra nodes coordinates
       ELSE                              !else
         x(:,i+6:i+9:3) = 0d0                          !nullify
       END IF
     END DO
   END IF
   ! compute in-plane gradient at each mid-side point of upper and lower faces
   IF( mtype == 6 .AND. quad ) cq = 0d0  ! initializes average in-plane metric tensor at each face
   DO ii=1,2 ! for each face
     i = 3*ii-2      !first node of the face  (1 or 4)
     IF( quad )THEN  ! for quad approacu
       j = i+5         !position previous to extra node at each face 6 or 9
       DO jj=1,3  !for each mid side point
         tq(:,1:2,jj,ii) = MATMUL(x(:,i:i+2),e%cdq(1:3,1:2,jj,ii))  !main element contribution
         IF( sides(jj) )THEN                     !add extra node contribution if exist
           tq(:,1,jj,ii) = tq(:,1,jj,ii) + x(:,j+jj)*e%cdq(4,1,jj,ii)
           tq(:,2,jj,ii) = tq(:,2,jj,ii) + x(:,j+jj)*e%cdq(4,2,jj,ii)
         END IF
         IF( mtype == 6 )THEN   !compute in-plane metric tensor for Rubbers
           cq(1,ii) = cq(1,ii) + DOT_PRODUCT(tq(:,1,jj,ii),tq(:,1,jj,ii)) !C11
           cq(2,ii) = cq(2,ii) + DOT_PRODUCT(tq(:,2,jj,ii),tq(:,2,jj,ii)) !C22
           cq(3,ii) = cq(3,ii) + DOT_PRODUCT(tq(:,1,jj,ii),tq(:,2,jj,ii)) !C12
         END IF
       END DO
     ELSE
       tq(:,1:2,1,ii) = MATMUL(x(:,i:i+2),e%cdq(1:3,1:2,1,ii))  !main element contribution
       IF( mtype == 6 )THEN    !compute in-plane metric tensor for Rubbers
         cq(1,ii) =  DOT_PRODUCT(tq(:,1,1,ii),tq(:,1,1,ii)) !C11
         cq(2,ii) =  DOT_PRODUCT(tq(:,2,1,ii),tq(:,2,1,ii)) !C22
         cq(3,ii) =  DOT_PRODUCT(tq(:,1,1,ii),tq(:,2,1,ii)) !C12
       END IF
     END IF
     !compute transvere shear strain components
     tas(:,:) = MATMUL(x(:,1:6),e%nfdas(:,:,ii))  !der x/x_3 at the 3 mid-side points
     IF( ii == 1 )THEN      !bottom face
       gas(:,1) = x(:,3) -x(:,2) !point 1 der x/tan SQR(2)
       gas(:,2) = x(:,1) -x(:,3) !point 2 der -x/eta
       gas(:,3) = x(:,2) -x(:,1) !point 3 der x/xita
       CALL bsma05(gas,tas,e%nfdas(:,:,1),bs(:,:,:,1),e%jacin(:,:,1),1)     !compute B_s matrix at sampling points
     ELSE                   !top face
       gas(:,1) = x(:,6) -x(:,5) !point 1 der x/tan SQR(2)
       gas(:,2) = x(:,4) -x(:,6) !point 2 der -x/eta
       gas(:,3) = x(:,5) -x(:,4) !point 3 der x/xita
       CALL bsma05(gas,tas,e%nfdas(:,:,2),bs(:,:,:,2),e%jacin(:,:,2),2)     !compute B_s matrix at sampling points
     END IF
     IF( mtype == 6 )THEN  !compute off-plane metric tensor for Rubbers
       DO g=1,3  !for each assumed strain point compute x_tan . x_X_3
         vas(g,ii) = DOT_PRODUCT(tas(:,g),gas(:,g))
       END DO
       vas(1,ii) = SUM(vas(:,ii))/3d0                                  !auxiliar value
       vas(2,ii) = +vas(1,ii) - vas(2,ii)                              !natural eta-x_3
       vas(3,ii) = -vas(1,ii) + vas(3,ii)                              !natural xita-x_3
       vas(1,ii) = e%jacin(1,1,ii)*vas(3,ii)+e%jacin(2,1,ii)*vas(2,ii) !x_1-x_3
       vas(2,ii) = e%jacin(1,2,ii)*vas(3,ii)+e%jacin(2,2,ii)*vas(2,ii) !x_2-x_3
     END IF
   END DO
   IF( mtype == 6 .AND. quad ) cq = cq/3d0    !in-plane average metric tensor at faces
   CALL bmat05q(e%cdq,bq,tq,quad) !in-plane b-matrix at faces
   ! EAS
   f3 = MATMUL(x(:,1:6),e%cartd)      ! x_3 at element center
   c33 = DOT_PRODUCT(f3,f3)           ! C33 at element center
   DO i=1,6
     b3(:,i) = f3*e%cartd(i)          ! B33 at element center
   END DO
   !
!  WRITE(58,"(' Calculo de la matriz de rigidez, elemento', i5)")e%numel
!  WRITE(58,"(' MATRICES B para el calculo de la matriz de rigidez')")
!  WRITE(58,"(' En el plano, cara inferior BM1')")
!  WRITE(58,"(3e15.6)")bq(:,:,:,1)
!  WRITE(58,"(' En el plano, cara superior BM2')")
!  WRITE(58,"(3e15.6)")bq(:,:,:,2)
!  WRITE(58,"(' De corte transversal Inferior BS1')")
!  WRITE(58,"(3e15.6)")bs(:,:,:,1)
!  WRITE(58,"(' De corte transversal Superior BS2')")
!  WRITE(58,"(3e15.6)")bs(:,:,:,1)
!  WRITE(58,"(' Deform. transversal Normal en el centro B3_0')")
!  WRITE(58,"(3e15.6)")b3
!  WRITE(58,"(' MATRIZ D para el calculo de la matriz de rigidez')")
!  WRITE(58,"(6e15.6)")d
!  WRITE(58,"(' Matriz B En cada punto de integracion')")
   ! Integration loop
   stiff = 0d0              !initializes stiffness matrix
   DO g=1,ngaus             !for each integration point
     fac = EXP(2d0*e%alpha*thf(g))                  !exp(2*alpha*zeta)
     voli = DOT_PRODUCT(sh(:,g),e%dvol)*wei(g)      !gauss point volume
     ! compute point constitutive matrix (tangent algorithmic)
     IF( nlayr == 0 )THEN
       SELECT CASE (mtype)
       CASE ( 1 )   ! for an isotropic one component material
         !IF( .NOT.elast )THEN
         !  lambd = e%gausv(14,g)         !Increment in Total Eff. plastic strain
         !  efpst = e%gausv(7,g) + lambd  !Total Eff. plastic strain
         !ELSE
         !  lambd = 0d0
         !  efpst = 0d0
         !END IF
         !CALL modp18(d,young,poiss,propi,efpst,lambd,.FALSE.,elast,newmt)
         !IF( natst )THEN
         !  ! Compute Metric tensor
         !  stran(1) = DOT_PRODUCT(t(:,1),t(:,1))
         !  stran(2) = DOT_PRODUCT(t(:,2),t(:,2))
         !  stran(3) = DOT_PRODUCT(t(:,3),t(:,3))
         !  stran(4) = DOT_PRODUCT(t(:,1),t(:,2))
         !  stran(5) = DOT_PRODUCT(t(:,1),t(:,3))
         !  stran(6) = DOT_PRODUCT(t(:,2),t(:,3))
         !  ! inverse of U^2
         !  a1  = stran(2)*stran(3) - stran(6)*stran(6)
         !  a2  =-stran(4)*stran(3) + stran(5)*stran(6)
         !  a3  = stran(4)*stran(6) - stran(5)*stran(2)
         !  aux1 = stran(1)*a1 + stran(4)*a2 + stran(5)*a3
         !  !IF(aux1 == 0d0) CALL runend('STIF18, singular matrix ')
         !  u2(1) = a1/aux1
         !  u2(2) = ( stran(1)*stran(3) - stran(5)*stran(5))/aux1
         !  u2(3) = ( stran(1)*stran(2) - stran(4)*stran(4))/aux1
         !  u2(4) = a2/aux1
         !  u2(5) = a3/aux1
         !  u2(6) = (-stran(1)*stran(6) + stran(4)*stran(5))/aux1
         !
         !ELSE  !small strain
           cm      = d
         !END IF
       CASE ( 6 )   !
         ! Compute Metric tensor: !interpolate from lower and upper surfaces
         stran((/1,2,4/)) = shape(1,g)*cq(:,1) + shape(2,g)*cq(:,2) !  in-plane components
         !stran(1) = shape(1,g)*cq(1,1) + shape(2,g)*cq(1,2)        !U^2(1,1)
         !stran(2) = shape(1,g)*cq(2,1) + shape(2,g)*cq(2,2)        !U^2(2,2)
         !stran(4) = shape(1,g)*cq(3,1) + shape(2,g)*cq(3,2)        !U^2(1,2)
         stran(5:6) = shape(1,g)*vas(1:2,1) + shape(2,g)*vas(1:2,2) !transverse shear components
         !stran(5) = shape(1,g)*vas(1,1) + shape(2,g)*vas(1,2)      !U^2(1,3)
         !stran(6) = shape(1,g)*vas(2,1) + shape(2,g)*vas(2,2)      !U^2(2,3)
         stran(3) = c33*fac                                         !U^2(3,3)
         CALL rubber3d(propi,stran,mat%matdef(8),mat=daux)          !compute D ==> daux
         daux = daux*voli                                           !D3 * dvol
       CASE DEFAULT !(7,92) !92 Simplified composite model
         cm      = d
       END SELECT
     ELSE  ! nlayr /= 0
       cm      = d
     END IF

     IF( nlayr == 0 .AND. mtype /= 6 )THEN    ! not for rubbers
       ! scale D matrix
       k = 0
       DO i=1,nstre
         DO j=i,nstre
           k = k+1
           daux(k) = voli*cm(i,j)
         END DO
       END DO
     END IF
     CALL bmat05(bmatx,bq,shape(:,g),bs,b3,fac,quad)        !interpolate B matrix
!    WRITE(58,"(3e15.6)")bmatx
!    WRITE(58,"(' volumen asociado al punto de integracion',e15.6)")voli

     CALL btdbpr(bmatx(1,1,1),daux(1),stiff(1),nvarl,nstre) ! B^T D B
   END DO
   ! Geometric matrix for in plane components
   IF( quad )THEN  !quad approach
     CALL kgmm05q(e%cdq,RESHAPE(e%se(1:6),(/3,2/)),stiff)
   ELSE            !standard approach
     CALL kgmm05(e%cdq,RESHAPE(e%se(1:6),(/3,2/)),stiff)
   END IF
   ! Kg for transverse shear
   CALL kgms05(e%nfdas,RESHAPE(e%se(7:10),(/2,2/)),e%jacin,stiff,nnode)
   ! Kg for transverse stress !(Correct? does not improve convergence.  ¿important?)
   !CALL kgmt05(e%cartd,e%se(11),stiff,nnode)
   ! condense EAS parameter
!  WRITE(58,"('factor k_alpha ',e15.5,' y vector h')")e%ka
!  WRITE(58,"(3e15.6)")e%h
   CALL cond05(stiff,e%h,e%ka,nvarl)
!  WRITE(58,"('Matriz de rigidez (triangulo superior)')")
!   k = 1
!   j = 18
!   DO i=1,18
!    WRITE(58,"(18e15.6)")stiff(k:j)
!     k = j+1
!     j = j+18-i
!   END DO
   ! add to global stiffness
   CALL stiaux(nnode,e%lnods,nvarl,ndime,stiff(1),force(1),gstif(1) )

   e => e%next
 END DO
 DEALLOCATE( stiff,bq,tq )
 RETURN

 END SUBROUTINE stif05
 SUBROUTINE cond05(k,h,ka,n)
 IMPLICIT NONE
 ! dummy arguments
 INTEGER(kind=4), INTENT(IN) :: n
 REAL(kind=8), INTENT(IN) :: ka,h(:)
 REAL(kind=8), INTENT(IN OUT) :: k(:)
 ! local variables
 INTEGER(kind=4) :: i,j,l
 REAL(kind=8) :: f
 l = 0
 DO i=1,n
   f = h(i)/ka
   DO j=i,n
     l = l+1
     k(l) = k(l) - f*h(j)
   END DO
 END DO

 RETURN

 END SUBROUTINE cond05
