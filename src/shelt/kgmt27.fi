 SUBROUTINE kgmt27(nnode,ndofn,nvarl, beta,estif,dn)
 !******************************************************************************
 !
 !*****this routine compute the geometric part of the tangent stiffness
 !                    for element 7 subtype=2 (membrane part)
 !******************************************************************************
 IMPLICIT NONE
 !                        routine parameters

 INTEGER (kind=4), INTENT(IN) :: nnode,ndofn,nvarl

 REAL (kind=8), INTENT(IN) :: beta(:),dn(:,:,:)
 REAL (kind=8), INTENT(IN OUT) :: estif(:)

 !                        local variables

 INTEGER (kind=4) j,m,ni,nj,idof,jdof,poses

 REAL    (kind=8)  coef1,coef2,ckgeo

 !                        local functions


 INTEGER (kind=4) poesti             !position i,j in estif(nxn)
 poesti(ni,nj,nvarl) = (2*nvarl-ni)*(ni-1)/2 + nj

 !     influence of membrane stresses

 DO m = 1,3
   DO ni = 1,nnode                                       ! node i
     idof = (ni-1)*ndofn
     !                                                 [ n11  n12 ]
     !         [coef1,coef2] = [d(n(i)/dx,d(n(i)/dy] * [ n21  n22 ]

     coef1 = beta(m)  *dn(ni,1,m) + beta(m+6)*dn(ni,2,m)
     coef2 = beta(m+6)*dn(ni,1,m) + beta(m+3)*dn(ni,2,m)
     !         for every node greater or equal than ni
     DO nj = ni,nnode                                    ! node j
       jdof = (nj-1)*ndofn
       !           ckgeo = [coef1,coef2] . [d(n(j)/dx,d(n(j)/dy]
       ckgeo = coef1*dn(nj,1,m) + coef2*dn(nj,2,m)
       DO j=1,3 !ndime
         poses = poesti(idof+j,jdof+j,nvarl)           ! diagonals,
         estif(poses) = estif(poses) + ckgeo
       END DO
     END DO                                            ! nj
   END DO                                              ! ni
 END DO

 END SUBROUTINE kgmt27
