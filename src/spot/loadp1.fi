 SUBROUTINE loadp1(ndime,nelem,igrav,loadf,gvect,gravy,heade)
 !********************************************************************
 !
 !***  evaluates gravity loads for SPOT elements
 !
 !********************************************************************
 IMPLICIT NONE
 INTEGER (kind=4) ndime,nelem,igrav
 REAL    (kind=8) loadf(:,:),gvect(:),gravy
 TYPE(ELE01), POINTER :: heade
 !        local variables
 INTEGER (kind=4) ielem,l,n,isec,osec
 REAL    (kind=8) weigt,mass
 TYPE(ELE01), POINTER :: e
 TYPE(section), POINTER :: sec
 LOGICAL :: newm

 IF(igrav /= 0 .AND. gravy /= 0.0) THEN

   osec = -1
   e => heade
   DO ielem=1,nelem

     isec  = e%matno
     newm = isec /= osec
     IF ( newm )THEN
       sec => psecs(isec)%p
       osec = isec
       mass = sec%rprop(5)  !translational mass
       weigt = gravy*mass/2d0
     END IF

     DO n = 1,2
       l = e%lnods(n)
       IF( l > 0 ) loadf(1:ndime,l) = loadf(1:ndime,l) + weigt*gvect
     END DO
     e => e%next
   END DO
 END IF
 RETURN
 END SUBROUTINE loadp1
