 SUBROUTINE load14 (igrav,force,gvect,gravy,heade)
 !********************************************************************
 !
 !***  evaluates gravity load vector for shell elements BST-CST (TLF)
 !
 !********************************************************************
 IMPLICIT NONE

 INTEGER (kind=4), PARAMETER :: nnode=3
 INTEGER (kind=4) :: igrav
 REAL (kind=8) :: force(:,:),gvect(3),gravy
 TYPE (ele14), POINTER  :: heade

 !        local variables

 INTEGER (kind=4) n,l,isec,osec
 REAL (kind=8) weigt,fvect(3),rhoel
 TYPE (ele14), POINTER :: elem
 TYPE (section), POINTER  :: sec

 IF(igrav /= 0) THEN       !if gravity is present

   osec = -1
   elem => heade           !point to first element
   DO
     IF (.NOT.ASSOCIATED (elem) ) EXIT    !all elements processed, Exit loop

     isec  = elem%matno                     !associated material
     IF( isec /= osec )THEN
       sec => psecs(isec)%p
       IF( sec%secty == 12 )THEN
         rhoel = sec%mtbas%prope(5)*sec%rprop(1)    !surface density
       ELSE
         rhoel = sec%rprop(5)                !surface density
       END IF
       weigt = gravy*rhoel                   !surface density
       osec = isec
     END IF
     IF(weigt /= 0.0d0) THEN                !if not zero

       fvect = elem%area1*weigt*gvect/3d0   !force vector at each node

       DO n=1,nnode                          !for each node (nnode=3)
         l = elem%lnods(n)                   !node number
         force(1:3,l) = force(1:3,l) + fvect !sum on global force vector
       END DO
     END IF

     elem => elem%next                       !point to next element
   END DO
 END IF

 RETURN
 END SUBROUTINE load14
