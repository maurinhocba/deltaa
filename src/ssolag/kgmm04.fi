 SUBROUTINE kgmm04 (cd,s,stiff,area)
 !
 ! compute geometric stiffness for SSOLAG
 !
 IMPLICIT NONE
 ! dummy arguments
 REAL (kind=8), INTENT(IN) ::  cd(:,:,:,:),  & !4,2,4,2 cartesian derivatives at side mid-points
                               area(:,:),    & !4,2 relative jacobians
                               s(:,:)          !3,2 ttt integrated stresses
 REAL (kind=8), INTENT(OUT) :: stiff(:)  !stiffness matrix
 ! local variables
 INTEGER (kind=4) i,j,k,l,m,n,pos,nv2,fa,kk
 REAL (kind=8) :: value,s11,s22,s12                  !diagonal value to add
 INTEGER (kind=4) poesti
 poesti(nv2,i,j) = (nv2-i)*(i-1)/2+j    ! position i,j in stiff(nxn)

 nv2 = 6*nnode  !twice the number of DOFs
 kk = 0         !first Face DOF position for first face
 DO fa=1,2     !for each face
   s11 = s(1,fa) ; s22 = s(2,fa) ; s12 = s(3,fa)  !face contribution
   DO k=1,4           !for each mid-side point (K)
     m = kk             ! initializes DOF position I
     DO i=1,4           ! for each node
       n = m            ! initializes DOF position J
       DO j=i,4         ! for each node (upper part)
         value = + ( s11* cd(i,1,k,fa)*cd(j,1,k,fa) + s22*cd(i,2,k,fa)*cd(j,2,k,fa)  &
                   + s12*(cd(i,2,k,fa)*cd(j,1,k,fa) + cd(j,2,k,fa)*cd(i,1,k,fa)))*area(k,fa)
         DO l=1,3    ! for each DOF (diagonal only)
           pos = poesti(nv2,m+l,n+l)   ! (I,J)
           stiff(pos) = stiff(pos) + value
         END DO
         n = n + 3   ! increment DOF position J
       END DO
       m = m + 3     ! increment DOF position I
     END DO
   END DO
   kk = 12        !first Face DOF position for 2nd face
 END DO

 RETURN
 END SUBROUTINE kgmm04
