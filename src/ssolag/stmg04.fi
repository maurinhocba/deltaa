 SUBROUTINE stmg04 (cd,s,stiff,area)
 !
 ! compute geometric stiffness for SSOLAG
 !
 IMPLICIT NONE
 ! dummy arguments
 REAL (kind=8), INTENT(IN) ::  cd(:,:,:,:),  & !4,2,4,2 cartesian derivatives at side mid-points
                               area(:,:),    & !4,2 relative jacobians
                               s(:,:)          !4,2 ttt integrated stresses
 REAL (kind=8), INTENT(OUT) :: stiff(:)  !stiffness matrix
 ! local variables
 INTEGER (kind=4) i,j,k,l,n,m,ii,jj,pos,nv2,fa
 REAL (kind=8) :: value,s12,ar                          !diagonal value to add
 INTEGER (kind=4) poesti
 poesti(nv2,i,j) = (nv2-i)*(i-1)/2+j    ! position i,j in stiff(nxn)

 nv2 = 6*nnode  !twice the number of DOFs
 DO fa=1,2      !for each face
   ii = 12*(fa-1)     !first Face DOF position - 1
   DO k=1,4            !for each side
     s12 = s(k,fa)       ! stabilization force
     DO l=1,4              ! for each side
       ar = -area(l,fa)      !side area
       IF( k == l ) ar = ar+1d0  !correct
       ar = ar*s12           !include force
       jj = ii               ! initializes DOF position I
       DO i=1,4              ! for each node of the face
         n = jj                ! initializes DOF position J
         DO j=i,4              ! for each node (upper part)
           value = + ar*(cd(i,2,l,fa)*cd(j,1,l,fa) + cd(j,2,l,fa)*cd(i,1,l,fa))
           DO m=1,3    ! for each DOF (diagonal only)
             pos = poesti(nv2,jj+m,n+m)   ! (I,J)
!write(58,"('f=',i1,' K=',i1,' L=',i1,' I=',i1,' J=',i1,' jj=',i2,' n=',i2,' m=',i1,' jj+m=',i2,' n+m=',i2,' pos=',i3)") &
!             fa,k,l,i,j,jj,n,m,jj+m,n+m,pos
             stiff(pos) = stiff(pos) + value
           END DO
           n = n + 3   ! increment DOF position J
         END DO
         jj = jj + 3     ! increment DOF position I
       END DO
     END DO
   END DO
 END DO

 RETURN
 END SUBROUTINE stmg04
