 SUBROUTINE stif04(heade, gstif, force, coora, small, ngaus, beta)

 ! 8-node solid-shell element (SSOLAG).
 ! use Green-Lagrange strains and 2nd Piola-Kirchhoff stresses

 USE ele18_db, ONLY : rubber3d
 IMPLICIT NONE
 ! dummy arguments
 INTEGER (kind=4), INTENT(IN) :: ngaus    !number of integration points

 LOGICAL, INTENT(IN) :: small     ! use Green-Lagrange strains
 REAL (kind=8), INTENT(IN) :: coora(:,:), &  ! present coordinates
                              beta(3)        !stabilization factors
 REAL (kind=8), INTENT(IN OUT) :: gstif(:),force(:)  !global stiffness and force vector
 TYPE (ele04), POINTER :: heade   !pointer to first element

 !Local Variables
 INTEGER (kind=4), PARAMETER :: ndime =3 , iel = 0
 LOGICAL, PARAMETER :: natst = .FALSE.    ! do not use large strains but for rubbers
 INTEGER (kind=4), PARAMETER:: algo(8,2) = (/ 0,1,3,4,6,7,9,10 , 0,2,3,5,6,8,9,0 /)

 TYPE (ele04), POINTER :: e               !pointer to an element
 LOGICAL  :: newmt,   &  !if present material different than previous
             plast,   &  !if plastic strains may be present
             elast       !if the material is elastic
 INTEGER (kind=4) :: i,j,g,k,ii,jj,l, &  !different indexes
                     isec,osec,       &  !present and old section
                     mtype,           &  !material type
                     nlayr,           &  !number of material components
                     nvarl,ielem         !number of DOFs in the patch

 REAL (kind=8) :: propi(13),       & !(nnn) material properties
                  young,poiss,     & !mechanical properties
                  deatht,          & !death time for plasticity (not used)
                  d(6,6),          & !full constitutive matrix (constant)
                  cm(6,6),         & !full constitutive matrix (pull-back)
                  daux(21),        & !upper diagonal constitutive matrix x dvol
                  chi(12)            !(12) Hill coefficients

 REAL (kind=8) efpst,             & !effective plastic strain
               f3(3),c33,b3(3,nnode), & !gradient 3, C33 and associate B matrix
               stres(6),          & !2nd P-K stresses for Geometric matrix
               stran(6),          & !C tensor to pull-back constitutive matrix
               voli,              & !Gauss point volume
               lambd,             & !iterative consistency parameter
               fac,               & !exp(2 x alpha x zeta)
               ka,stm,sts           !stabilization factor

 REAL (kind=8) :: bmatx(ndime,nnode,nstre), &   !global B matrix (to be interpolated)
                  x(ndime,nnode),           &   !local coordinates
                  cq(3,2),                  &   !in-plane metric tensor at faces
                  bq(3,4,3,2),              &   !in-plane B matrix at faces
                  bm(12,4,2),               &   !in-plane B-12 matrix at mid-point on faces
                  tq(ndime,2,4,2),          &   !in-plane tangent vectors at sides
                  stiff(300)                    !stiffness matrix
 !           variables for interpolation into the Gauss points
 REAL (kind=8) :: thf(ngaus),   & !G-P positions
                  wei(ngaus),   & !G-P weigths
                  sh(3,ngaus),  & !Quadratic approach functions for Jacobian
                  shape(2,ngaus)  !Linear approach for C components
 ! variables for assumed strain transverse shear
 REAL (kind=8) :: tas(3,4),      & ! der x/x_3 at Assumed Strain points
                  gas(3,4),      & ! der x/tan at Assumed Strain points
                  vas(4,2),va(4),& ! aux and C23,C13 at faces
                  bs(3,8,2,2),bh(24,2,2)  ! Bs matrix at faces and stabilization part


 TYPE (section), POINTER :: sec   !pointer to section
 TYPE (mater), POINTER :: mat     !pointer to material
 !TYPE (mater), POINTER :: matm,matf !pointer to a material data

 INTERFACE
   INCLUDE 'modp18.h'
 END INTERFACE

 nvarl = ndime*nnode  !nnode*ndime number of element DOFs

 CALL gaussq(ngaus,thf(1),wei(1)) !integration points through the thickness
 wei = wei*4d0
 DO g=1,ngaus
   ! compute sh(3,ngaus) to evaluate jacobian
   sh(1,g) = -0.5d0*(1d0-thf(g))*thf(g)
   sh(2,g) =  1d0-thf(g)**2
   sh(3,g) =  0.5d0*(1d0+thf(g))*thf(g)
   ! compute shape(2,ngaus) to interpolate from upper and lower faces
   shape(1,g) =  0.5d0*(1d0-thf(g))
   shape(2,g) =  0.5d0*(1d0+thf(g))
 END DO

 !***  Compute Element Stiffness matrix for all elements
 bmatx = 0d0           !initializes B-matrix
 osec = -1             !initialize to a non-existent section
 e => heade            !point to first element

 DO
   IF (.NOT.ASSOCIATED (e) ) EXIT    !last element processed => EXIT
   isec = e%matno                    !associated section to element
   ielem = e%numel
   newmt = osec /= isec              !a different material must be considered
   IF(newmt)THEN                     !if new material, get properties
     osec = isec                     !keep present material
     sec => psecs(isec)%p            !point to section
     nlayr = sec%iprop(1)             !number of layers
     !nvar  = sec%iprop(2)             !total number of internal variables

     !************************** STANDARD SOLID *****************************************
     IF( nlayr == 0 )THEN            !standard solid section (not layered)

       mat => sec%mtbas                !point to associated material
       mtype = mat%mtype               !type of base material
       elast = mat%matdef(3) == 1      !elastic material
       !is    = mat%matdef(4)           !isotropic hardening model
       ! ------------------------------------------------------------------------------------
       ! ONLY MTYPES 1, 5, 6, 7 and 92 ARE ALLOWED
       SELECT CASE (mtype)
       ! ------------------------------------------------------------------------------------
       CASE (1)                        !Isotropic Elastic - Anisotropic Plastic  ( mtype == 1)
         !IF( .NOT.elast ) THEN
         !  ! e1, nu1, uniaxial, efren, consn, r, exponent m, hill 79
         !  propi(1:4) = mat%propp(1:4)       ! isotropic hardening parameters
         !  propi(5) = REAL( mat%matdef(4),8) ! isotropic hardening model
         !  chi    = mat%propp(16:27)         ! hill coefficients
         !  IF( mat%matdef(6) == 1  ) chi(1:6) = chi(7:12)  !associative
         !  deatht = mat%propp(5)             !end of plasticity
         !  plast = propi(1) > 0  !consider plasticity ?
           young = mat%prope(1)          ! young modulus
           poiss = mat%prope(2)          ! poisson ratio
           stm = 4d0*young/(1d0+poiss)*beta(1)
           sts = 2d0/9d0*young/(1d0+poiss)*beta(2)
         !  ! von Mises plasticity IF( mat%matdef(3) <= 2 )
           CALL modp18(d,young,poiss,propi,0d0,0d0,.FALSE.,elast,newmt)
         !ELSE
           plast = .FALSE.
        ! END IF
         ! ------------------------------------------------------------------------------------
       CASE (5)                        !Orthotropic Elastic - No Plasticity      ( mtype == 5)
         d = RESHAPE( mat%prope(45:80), (/6,6/) )
         stm = 8d0*d(4,4)*beta(1)
         sts = 2d0/9d0*(d(5,5)+d(6,6))*beta(2)
         elast = .TRUE.
         plast = .FALSE.                        !consider plasticity ?
       ! ------------------------------------------------------------------------------------
       CASE (6)                        !Hyperelastic isotropic rubber            ( mtype == 6)
         propi(1:12) = mat%prope(7:18)     !elastic properties
         elast = .TRUE.                    !elastic only
         plast = .FALSE.                   !consider plasticity ?
       ! ------------------------------------------------------------------------------------
       END SELECT

       !************************** LAYERED SOLID *************************************************
     ELSE  !nlayr >= 1      layered solid (at integration point level)
       !thick = sec%rprop(1)             !original thickness
       !minstr= sec%rprop(4)             !strain threshold to use TTTI

       !nl=1
       elast = .TRUE. !sec%iprop(3) == 0               !elastic problem
       plast = .NOT.elast                      !consider plasticity ?
       d = RESHAPE( sec%rprop(6:41), (/6,6/) )                 !linear elastic integrated matrix
       !IF( plast )THEN  !plastic lamina
       !  IF( ALLOCATED ( rr ) )DEALLOCATE( thickl,zposl,rr,matsl,lvari )
       !  ALLOCATE( thickl(nlayr),zposl(nlayr),rr(5,nlayr),matsl(3,nlayr),lvari(nlayr) )
       !  i = 42                                !pointer to layer thickness
       !  CALL vecasi(nlayr,sec%rprop(i),thickl(1))        !layer thickness
       !  i = i+2*nlayr                         !pointer to layer z position
       !  !CALL vecasi(nlayr,sec%rprop(i),zposl(1))         !layer z position
       !  i = i+nlayr                           !pointer to layer rotation matrix
       !  CALL vecasi(nlayr*5,sec%rprop(i),rr(1,1))        !layer rotation matrix
       !
       !  lvari(1:nlayr)= sec%iprop(nlayr+5:2*nlayr+4)
       !  DO il=1,nlayr
       !    mate= sec%iprop(4+il)
       !    mat => pmats(mate)%p        !search layer material
       !    matsl(1,il)%p => mat
       !    SELECT CASE (mat%mtype)
       !    CASE ( 91,92 )
       !      !point to component materials data bases
       !      matsl(2,il)%p => pmats(mat%matdef(4))%p  !matm
       !      matsl(3,il)%p => pmats(mat%matdef(5))%p  !matf
       !    CASE DEFAULT
       !      NULLIFY( matsl(2,il)%p )
       !      NULLIFY( matsl(3,il)%p )
       !    END SELECT
       !  END DO
       !END IF
       !osec = -1                              !recompute constants
     END IF
     newmt = .FALSE.               !same material than previous ?

   END IF !newmt
   !!            HERE
   x(:,1:nnode) = coora(:,e%lnods(1:nnode))     !nodes of the element
   ! compute in-plane gradient at each mid-side point of upper and lower faces
   IF( mtype == 6 ) cq = 0d0  ! initializes average in-plane metric tensor at each face
   DO ii=1,2 ! for each face
     i = 4*ii-3      !first node of the face  (1 or 5)

     DO jj=1,4  !for each mid side point
       tq(:,1:2,jj,ii) = MATMUL(x(:,i:i+3),e%cdq(1:4,1:2,jj,ii))  !main element contribution
       IF( mtype == 6 )THEN    !compute in-plane metric tensor for Rubbers
         cq(1,ii) = cq(1,ii) + DOT_PRODUCT(tq(:,1,1,ii),tq(:,1,1,ii))*e%area(jj,ii)  !C11
         cq(2,ii) = cq(2,ii) + DOT_PRODUCT(tq(:,2,1,ii),tq(:,2,1,ii))*e%area(jj,ii)  !C22
         cq(3,ii) = cq(3,ii) + DOT_PRODUCT(tq(:,1,1,ii),tq(:,2,1,ii))*e%area(jj,ii)  !C12
       END IF
     END DO
     !compute transvere shear strain components
     tas(:,:) = MATMUL(x,e%nfdas(:,:,ii))  !der x/x_3 at the 4 mid-side points
     IF( ii == 1 )THEN      !bottom face
       gas(:,1) = (x(:,4) -x(:,1))/2d0 !point A der x/eta
       gas(:,2) = (x(:,2) -x(:,1))/2d0 !point B der x/xita
       gas(:,3) = (x(:,3) -x(:,2))/2d0 !point C der x/eta
       gas(:,4) = (x(:,3) -x(:,4))/2d0 !point D der x/xita
       CALL bsma04(gas,tas,e%nfdas(:,:,1),bs(:,:,:,1),e%jacin(:,:,1),1,bh(:,:,1))     !compute B_s matrix at sampling points
     ELSE
       gas(:,1) = (x(:,8) -x(:,5))/2d0 !point A der x/eta
       gas(:,2) = (x(:,6) -x(:,5))/2d0 !point B der x/xita
       gas(:,3) = (x(:,7) -x(:,6))/2d0 !point C der x/eta
       gas(:,4) = (x(:,7) -x(:,8))/2d0 !point D der x/xita
       CALL bsma04(gas,tas,e%nfdas(:,:,2),bs(:,:,:,2),e%jacin(:,:,2),2,bh(:,:,2))     !compute B_s matrix at sampling points
     END IF
     IF( mtype == 6 )THEN  !compute off-plane metric tensor for Rubbers
       DO g=1,4  !for each assumed strain point compute x_tan . x_X_3
         va(g) = DOT_PRODUCT(tas(:,g),gas(:,g))
       END DO
       vas(3,ii) =(va(2)+va(4))/2d0                                    !C_xi3
       vas(4,ii) =(va(1)+va(3))/2d0                                    !C_eta3
       vas(1,ii) = e%jacin(1,1,ii)*vas(3,ii)+e%jacin(2,1,ii)*vas(4,ii) !x_1-x_3
       vas(2,ii) = e%jacin(1,2,ii)*vas(3,ii)+e%jacin(2,2,ii)*vas(4,ii) !x_2-x_3
       vas(3,ii) =-va(2)+va(4)                                         !C_xi3_H
       vas(4,ii) =-va(1)+va(3)                                         !C_eta3_H
     END IF
   END DO

   CALL bmat04q(e%cdq,bq,tq,bm,e%area) !in-plane b-matrix at faces and stabilization

   ! EAS
   !f3 = (/0d0,0d0,1d0/)
   f3 = MATMUL(x,e%cartd)             ! x_3 at element center
   c33 = DOT_PRODUCT(f3,f3)           ! C33 at element center
   DO i=1,nnode
     b3(:,i) = f3*e%cartd(i)          ! B33 at element center
   END DO
   IF( ielem == iel )THEN
     WRITE(58,"(' valores para el elemento ',i5)")ielem
     WRITE(58,"(' coordenadas nodales X')")
     WRITE(58,"(i5,3e15.5)")(e%lnods(i),x(:,i),i=1,nnode)
     WRITE(58,"(' derivadas cartesianas de las 4 funciones de forma en los lados CDQ')")
     WRITE(58,"(' cara lado    N1,1        N2,1        N3,1        N4,1    ',&
              &           '    N1,2        N2,2        N3,2        N4,2')")
     WRITE(58,"(i5,i5,8e12.4)")((ii,jj,e%cdq(:,:,jj,ii),jj=1,4),ii=1,2)
     WRITE(58,"(' factores de area en cada cara en cada lado AREA')")
     WRITE(58,"(' cara lado   Factor ')")
     WRITE(58,"(i5,i5,e12.4)")((ii,jj,e%area(jj,ii),jj=1,4),ii=1,2)
     WRITE(58,"(' Jacobianos inversos en cara en cada lado JACIN')")
     WRITE(58,"(' cara    JACIN ')")
     WRITE(58,"(i5,4e12.4)")(ii,e%jacin(:,:,ii),ii=1,2)
     WRITE(58,"(' Dx/DX en cada cara en cada lado TQ')")
     WRITE(58,"(' cara lado                  T1                        T2 ')")
     WRITE(58,"(i5,i5,6e12.4)")((ii,jj,tq(:,:,jj,ii),jj=1,4),ii=1,2)
     WRITE(58,"(' Matrices Bs en cada cara (centro) para cada direccion cartesiana BS')")
     WRITE(58,"(' cara  dir    Bs')")
     WRITE(58,"(2i5,24e12.4)")((ii,jj,bs(:,:,jj,ii),jj=1,2),ii=1,2)
     WRITE(58,"(' Matrices Bm en cada cara (centro) para cada Deformacion BQ')")
     WRITE(58,"(' cara  def    BQ')")
     WRITE(58,"(2i5,12e12.4)")((ii,jj,bq(:,:,jj,ii),jj=1,3),ii=1,2)
     WRITE(58,"(' Matriz B3 en el centro del elemento')")
     WRITE(58,"(i5,3e12.4)")(ii,b3(:,ii),ii=1,8)
     WRITE(58,"(' PARA CADA PUNTO DE GAUSS A LO LARGO DEL EJE Matriz B')")
   END IF
   ! Integration loop
   stiff = 0d0              !initializes stiffness matrix
   DO g=1,ngaus             !for each integration point
     fac = EXP(2d0*e%alpha*thf(g))                  !exp(2*alpha*zeta)
     voli = DOT_PRODUCT(sh(:,g),e%dvol)*wei(g)      !gauss point volume
     ! compute point constitutive matrix (tangent algorithmic)
     IF( nlayr == 0 )THEN
       SELECT CASE (mtype)
       CASE ( 1 )   ! for an isotropic one component material
         !IF( .NOT.elast )THEN
         !  lambd = e%gausv(14,g)         !Increment in Total Eff. plastic strain
         !  efpst = e%gausv(7,g) + lambd  !Total Eff. plastic strain
         !ELSE
         !  lambd = 0d0
         !  efpst = 0d0
         !END IF
         !CALL modp18(d,young,poiss,propi,efpst,lambd,.FALSE.,elast,newmt)
         IF( natst )THEN
           ! Compute Metric tensor
           !stran(1) = DOT_PRODUCT(t(:,1),t(:,1))
           !stran(2) = DOT_PRODUCT(t(:,2),t(:,2))
           !stran(3) = DOT_PRODUCT(t(:,3),t(:,3))
           !stran(4) = DOT_PRODUCT(t(:,1),t(:,2))
           !stran(5) = DOT_PRODUCT(t(:,1),t(:,3))
           !stran(6) = DOT_PRODUCT(t(:,2),t(:,3))
           ! inverse of U^2
           !a1  = stran(2)*stran(3) - stran(6)*stran(6)
           !a2  =-stran(4)*stran(3) + stran(5)*stran(6)
           !a3  = stran(4)*stran(6) - stran(5)*stran(2)
           !aux1 = stran(1)*a1 + stran(4)*a2 + stran(5)*a3
           !IF(aux1 == 0d0) CALL runend('STIF18, singular matrix ')
           !u2(1) = a1/aux1
           !u2(2) = ( stran(1)*stran(3) - stran(5)*stran(5))/aux1
           !u2(3) = ( stran(1)*stran(2) - stran(4)*stran(4))/aux1
           !u2(4) = a2/aux1
           !u2(5) = a3/aux1
           !u2(6) = (-stran(1)*stran(6) + stran(4)*stran(5))/aux1
           ! what are the modifications to be made?
           cm      = d
         ELSE  !small strain
           cm      = d
         END IF
       !CASE ( 5 )   ! for an orthotropic one component material
       !    cm      = d
       CASE ( 6 )   !
         ! Compute Metric tensor: !interpolate from lower and upper surfaces
         stran((/1,2,4/)) = shape(1,g)*cq(:,1) + shape(2,g)*cq(:,2) !  in-plane components
         !stran(1) = shape(1,g)*cq(1,1) + shape(2,g)*cq(1,2)        !U^2(1,1)
         !stran(2) = shape(1,g)*cq(2,1) + shape(2,g)*cq(2,2)        !U^2(2,2)
         !stran(4) = shape(1,g)*cq(3,1) + shape(2,g)*cq(3,2)        !U^2(1,2)
         stran(5:6) = shape(1,g)*vas(1:2,1) + shape(2,g)*vas(1:2,2) !transverse shear components
         !stran(5) = shape(1,g)*vas(1,1) + shape(2,g)*vas(1,2)      !U^2(1,3)
         !stran(6) = shape(1,g)*vas(2,1) + shape(2,g)*vas(2,2)      !U^2(2,3)
         stran(3) = c33*fac                                         !U^2(3,3)
         CALL rubber3d(propi,stran,mat%matdef(8),mat=daux)          !compute D ==> daux
         daux = daux*voli                                           !D3 * dvol
       CASE DEFAULT ! ??
         cm      = d
       END SELECT
     ELSE  ! nlayr /= 0
       cm      = d
     END IF

     IF( nlayr == 0 .AND. mtype /= 6 )THEN    ! not for rubbers
       ! scale D matrix
       k = 0
       DO i=1,nstre
         DO j=i,nstre
           k = k+1
           daux(k) = voli*cm(i,j)
         END DO
       END DO
     END IF
     CALL bmat04(bmatx,bq,shape(:,g),bs,b3,fac)             !interpolate B matrix
     IF( ielem == iel )THEN
       WRITE(58,"(' G ',i5)")g
       WRITE(58,"(3e15.5)")bmatx
     END IF
     CALL btdbpr(bmatx(1,1,1),daux(1),stiff(1),nvarl,nstre) ! B^T D B
   END DO
   IF( ielem == iel )THEN
     WRITE(58,"(' Matriz de rigidez luego de la integración ')")
     ii = 1
     DO i=1,24
       jj = ii+24-i
       WRITE(58,"(24e12.4)")stiff(ii:jj)
       ii = jj+1
     END DO
   END IF
   IF( mtype == 6 )THEN
      stm = daux(12)*beta(1)/voli
      sts = daux(12)*beta(2)/voli
   END IF
       ! Geometric matrix for in plane components
   CALL kgmm04(e%cdq,RESHAPE(e%se(1:6),(/3,2/)),stiff,e%area)
   !! Kg for transverse shear
   !CALL kgms04(e%nfdas,RESHAPE(e%se(7:10),(/2,2/)),e%jacin,stiff,nnode)
   !! Kg for transverse stress !
   !CALL kgmt04(e%cartd,e%se(11),stiff,nnode)
   !Membrane Stabilization
   ka = e%dvol(2)*stm
   CALL stbm04(stiff,bm,ka,e%area)
   IF( ielem == iel )THEN
     WRITE(58,"(' Matriz de rigidez luego de la estabilizacion membranal KA=vol_g * STM',e12.4)")ka
     ii = 1
     DO i=1,24
       jj = ii+24-i
       WRITE(58,"(24e12.4)")stiff(ii:jj)
       ii = jj+1
     END DO
   END IF
   !CALL stmg04(e%cdq,RESHAPE(e%se(12:19),(/4,2/)),stiff,e%area)
   !Shear Stabilization
   ka = e%dvol(2)*sts
   CALL stbs04(stiff,bh,ka,e%jacin)
   IF( ielem == iel )THEN
     WRITE(58,"(' Matriz de rigidez luego de la estabilizacion del corte KA=vol_g * STS',e12.4)")ka
     ii = 1
     DO i=1,24
       jj = ii+24-i
       WRITE(58,"(24e12.4)")stiff(ii:jj)
       ii = jj+1
     END DO
   END IF
   ! condense EAS parameter
   CALL cond04(stiff,e%h,e%ka,nvarl)
   IF( ielem == iel )THEN
     WRITE(58,"(' vector h ')")
     WRITE(58,"(3e15.5)")e%h
     WRITE(58,"(' Matriz de rigidez luego de la condensacion del grado de ALPHA, Ka', e15.5)")ka
     ii = 1
     DO i=1,24
       jj = ii+24-i
       WRITE(58,"(24e12.4)")stiff(ii:jj)
       ii = jj+1
     END DO
   END IF
   ! add to global stiffness
   CALL stiaux(nnode,e%lnods,nvarl,ndime,stiff(1),force(1),gstif(1) )

   e => e%next
 END DO
!  print elemental stiffness matrix
!k = 0
!DO i=1,24
!  DO j=i,24
!    k = k + 1
!    bh(j,1,1) = stiff(k)
!  END DO
!  l = i
!  DO j=1,i-1
!    bh(j,1,1) = stiff(l)
!    l = l + 24 - j
!  END DO
!  WRITE(58,"(24e22.14)")bh(:,1,1)
!END DO
RETURN

 END SUBROUTINE stif04


 SUBROUTINE cond04(k,h,ka,n)
 IMPLICIT NONE
 ! dummy arguments
 INTEGER(kind=4), INTENT(IN) :: n
 REAL(kind=8), INTENT(IN) :: ka,h(:)
 REAL(kind=8), INTENT(IN OUT) :: k(:)
 ! local variables
 INTEGER(kind=4) :: i,j,l
 REAL(kind=8) :: f
 l = 0
 DO i=1,n
   f = h(i)/ka
   DO j=i,n
     l = l+1
     k(l) = k(l) - f*h(j)
   END DO
 END DO

 RETURN

 END SUBROUTINE cond04


 SUBROUTINE stbm04(s,b,ka,area)
 IMPLICIT NONE
 ! dummy arguments
 REAL(kind=8), INTENT(IN) :: ka,b(:,:,:),area(:,:)
 REAL(kind=8), INTENT(IN OUT) :: s(:)
 ! local variables
 INTEGER(kind=4) :: i,j,k,l,m,l0
 REAL(kind=8) :: f

 l0 = 0   !first position in stiff for first face
 DO m=1,2  !for each face
   DO k=1,4        !for each mid-side point
     l = l0        !initializes position in Stiff
     DO j=1,12     !for each DOF in the face
       f = b(j,k,m)*ka*area(k,m)
       DO i=j,12
         l = l+1    !row
         s(l) = s(l) + f*b(i,k,m)
       END DO
       IF(m == 1) l=l+12 !for first face skip DOFs in second face
     END DO
   END DO
   l0 = 222        !first position in stiff for second face
 END DO

 RETURN

 END SUBROUTINE stbm04

 SUBROUTINE stbs04(s,bh,ka,ji)
 IMPLICIT NONE
 ! dummy arguments
 REAL(kind=8), INTENT(IN) :: ka,bh(:,:,:),ji(:,:,:)
 REAL(kind=8), INTENT(IN OUT) :: s(:)
 ! local variables
 INTEGER(kind=4) :: i,j,l,m
 REAL(kind=8) :: a,b,f1,f2


 DO m=1,2  !for each face
   a = (ji(1,1,m)*ji(1,1,m) + ji(2,1,m)*ji(2,1,m))*ka
   b = (ji(1,2,m)*ji(1,2,m) + ji(2,2,m)*ji(2,2,m))*ka
   l = 0
   DO i=1,24       !for each DOF
     f1 = bh(i,1,m)*a
     f2 = bh(i,2,m)*b
     DO j=i,24
       l = l+1    !row
       s(l) = s(l) + f1*bh(j,1,m) + f2*bh(j,2,m)
     END DO
   END DO
 END DO

 RETURN

 END SUBROUTINE stbs04
