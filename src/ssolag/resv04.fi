 SUBROUTINE resv04( heade, coora, resid, ierr, ttime, small, ngaus, beta)
 !-------------------------------------------------------------------
 !
 !          EVALUATION OF INTERNAL FORCES ELEMENT T-L 3-D SOLID
 !
 !-------------------------------------------------------------------
 USE ele18_db, ONLY : lgst18,eige18,rubber3d
 USE npo_db, ONLY : coori  !coordinates at previous iteration
 IMPLICIT NONE

 ! dummy arguments
 INTEGER (kind=4), INTENT(OUT) :: ierr        !flag for error detection
 INTEGER (kind=4), INTENT(IN) :: ngaus        !number of integration points
 REAL(kind=8),INTENT (IN) :: coora(:,:),    & !present coordinates
                             beta(:),       & !stabilization factors
                             ttime            !present total time
 REAL(kind=8),INTENT (IN OUT) :: resid(:,:)   !internal nodal forces
 TYPE (ele04), POINTER  :: heade              !pointer to first element
 LOGICAL, INTENT(IN) :: small      !.TRUE. if Green strains will be used

 ! local variables
 INTEGER (kind=4), PARAMETER :: ndime=3, iel=0
 ! variables for assumed strain transverse shear
 REAL (kind=8) :: tas(3,4),      & ! der x/x_3 at Assumed Strain points
                  gas(3,4),      & ! der x/tan at Assumed Strain points
                  vas(4,2),va(4),& ! strains at Assumed Strain Points
                  bs(3,8,2,2),bh(24,2,2)   ! Bs matrix at face centers, Stab matrix
 TYPE (ele04), POINTER :: e      !pointer to an element data
 LOGICAL :: newmt,   & ! TRUE if material constant computation necessary
            elast,   & ! TRUE if material is elastic
            plast      ! TRUE if plasticity is to be considered
 INTEGER (kind=4) :: osec,     & !associated material of previous element
                     numpt,    & !number of points in curve
                     ndofe,    & !number of DOFs
                     mtype         !associated material type
 REAL (kind=8) ::  fr(9),      & !Flow rule coefficients
                   km,gm,      & !bulk modulus and shear modulus
                   eprop(12),  & !elastic Material properties
                   props(7),   & !plastic Material properties
                   thick,      & !thickness (original)
                   minstr,     & !minimum strain to integrate trougth the thickness
                   dm(36),     & !Elastic integrated constitutive matrix
                   ch(6)         !Hill-48 coefficients

 INTEGER (kind=4) ielem,             & !element number
                  is,                & !isotropic hardening model
                  isec,              & !associated material
                  flag,              & !flag for eigen-decomposition
                  i,ii,jj,j,g,n        !different indexes

 INTEGER (kind=4) nl,nlayr,  & !number of layers
                  oldm,      & !old material label
                  mate,      & !material label
                  ilv,jlv,   & !indexes for layer varin
                  il,        & !layer index
                  nvar         !total number of internal variables

 REAL (kind=8) stral(6),   & !local strains
               strel(6),   & !local streses
               staux(6),   & !local streses auxiliar
               str(6),     & !stresses (different measures)
               sig(6),     & !stresses (different measures)
               stran(6),   & !C=U^2  also Log strains
               aux,voli,   & !auxiliar value
               efpst,      & !effective plastic strain
               strpl(6),   & !plastic strain (previous step)
               r(3,3),lb(3),& !eigen-pair in local system
               delta         !volumetric strain/3, element volume
 REAL (kind=8) f3(3),c33,c33z,b3(3,nnode),h(3,nnode),ka,d3(nstre),fac, &  !EAS
               seq(11),deq(11),ralpha,stm,sts
 REAL (kind=8) :: u(ndime,nnode),          &  !incremental displacements
                  x(ndime,nnode),          &  !element coordinates
                  rl(ndime,nnode),         &  !nodal internal equivalent forces
                  cq(3,2)                     !average metric tensor at each face
 REAL (kind=8) :: tq(3,2,4,2), &  !(ndime,2,3,2) local in-plane deformation gradient at face sides
                  bq(3,4,3,2), &  !(ndime,nnode,3,2) in-plane B-matrix at face centers
                  bm(12,4,2),eh(4,2)  !in-plane B12-matrix at mid-point sides for stabilization

 REAL (kind=8) :: thf(ngaus), wei(ngaus), sh(3,ngaus), shape(2,ngaus)   !thickness gauss integration

 LOGICAL :: pflag,   &! TRUE if plastic flow in the step
            ttti,    &! TRUE if Trougth The Thickness Integration is necessary
            flag2     ! TRUE if plastic at any gauss point

 REAL (kind=8), POINTER :: val(:,:) ! curve for IHPOINT
 REAL (kind=8)          :: elen     ! Damage length - For damage subroutine - XMG

 TYPE (section), POINTER :: sec  !pointer to a section data
 TYPE (mater), POINTER   :: mat  !pointer to a material data

 TYPE (pmater), POINTER :: matsl(:)    !array of pointers to materials data  ¿?
 ! allocatable arrays : MUST be replaced by any STATIC variables to avoid run-time memory increase
 INTEGER (kind=4), ALLOCATABLE ::  lvari(:)
 REAL (kind=8), ALLOCATABLE ::  thickl(:),zposl(:),rr(:,:)
 REAL (kind=8), ALLOCATABLE :: varin(:,:)                      !internal variables

 INTERFACE
   INCLUDE 'corr18.h'
 END INTERFACE

 ndofe = ndime*nnode     ! number of DOFs
 !integration points through the thickness
 CALL gaussq(ngaus,thf(1),wei(1))
 wei = wei*4d0
 ! compute shape(2,ngaus) to interpolate
 ! compute sh(3,ngaus) to evaluate jacobian
 DO g=1,ngaus
   sh(1,g) = -0.5d0*(1d0-thf(g))*thf(g)  !quadratic 1-D Lagrangian polynomials (for jacobian)
   sh(2,g) =  1d0-thf(g)**2
   sh(3,g) =  0.5d0*(1d0+thf(g))*thf(g)
   shape(1,g) =  0.5d0*(1d0-thf(g))      !linear 1-D Lagrangian polynomials (for interpolation)
   shape(2,g) =  0.5d0*(1d0+thf(g))
 END DO

 !***  Computes nodal internal forces
 osec = -1                           !no previous material
 e => heade                          !point to first element in the list
 DO                                  !loop over all the elements
   IF (.NOT.ASSOCIATED (e) ) EXIT    !last element processed => EXIT
   ielem = e%numel                   !element number
   isec = e%matno                    !associated material
   !     SET MATERIAL CONSTANTS
   newmt = osec /= isec              !same material than previous ?
   IF(newmt)THEN                     !if new material, get properties
     osec = isec                     !keep present material
     sec => psecs(isec)%p             !point to section
     nlayr = sec%iprop(1)             !number of layers
     nvar  = sec%iprop(2)             !total number of internal variables

     !************************** STANDARD SOLID *****************************************
     IF( nlayr == 0 )THEN            !standard solid section (not layered)

       mat => sec%mtbas                !point to associated material
       mtype = mat%mtype               !type of base material
       elast = mat%matdef(3) == 1      !elastic material
       is    = mat%matdef(4)           !isotropic hardening model
       ! ------------------------------------------------------------------------------------
       ! ONLY MTYPES 1, 5, 6 ARE ALLOWED
       SELECT CASE (mtype)
       ! ------------------------------------------------------------------------------------
       CASE (1)                        !Isotropic Elastic - Anisotropic Plastic  ( mtype == 1)
         km = mat%prope(4)*3d0              ! bulk modulus * 3
         gm = mat%prope(3)                  ! shear modulus
         stm = 8d0*gm*beta(1)
         sts = 4d0/9d0*gm*beta(2)
         aux = (km - gm*2d0)/3d0            ! lambda (Lamé parameter)
         d3 = (/ aux,aux,aux+2d0*gm, 0d0,0d0,0d0/)  ! Elastic column 3 of D
         IF( .NOT.elast ) THEN
           ! e1, nu1, uniaxial, efren, consn, r, exponent m, hill 79
           props(1:7) = mat%propp(1:7)        !
           ! for anisotropic (PLANAR) plasticity
           ch = mat%propp(16:21)              !Hill yield function coefficients
           ! A matrix, derivative of yield function (associative only)
           fr(1) =  ch(2)+ch(3) + 0.5d0       !0.5d0 added to make it diagonal
           fr(2) = -ch(3)       + 0.5d0       ! in case of von Mises
           fr(3) = -ch(2)       + 0.5d0       ! it is not essential
           fr(4) =  ch(1)+ch(3) + 0.5d0
           fr(5) = -ch(1)       + 0.5d0
           fr(6) =  ch(1)+ch(2) + 0.5d0
           fr(7) = 2d0*ch(6)
           fr(8) = 2d0*ch(4)
           fr(9) = 2d0*ch(5)
           plast = props(5) > ttime                 !consider plasticity ?
           IF ( is == 5 )THEN             !points defined yield curve
             val => mat%chead%val
             numpt = mat%chead%np
           ELSE
             NULLIFY (val)
             numpt = 0
           END IF
         ELSE
           plast = .FALSE.
         END IF
         ! ------------------------------------------------------------------------------------
       CASE (5)                        !Orthotropic Elastic - No Plasticity      ( mtype == 5)
         eprop(1) = mat%prope(45)      !(1,1)   11-11
         eprop(2) = mat%prope(46)      !(2,1)   11-22
         eprop(3) = mat%prope(47)      !(3,1)   11-33
         eprop(4) = mat%prope(52)      !(2,2)   22-22
         eprop(5) = mat%prope(53)      !(3,2)   22-33
         eprop(6) = mat%prope(59)      !(3,3)   33-33
         eprop(7) = mat%prope(66)      !(4,4)   12-12
         eprop(8) = mat%prope(73)      !(5,5)   13-13
         eprop(9) = mat%prope(80)      !(6,6)   23-23
         d3 = (/ eprop(3),eprop(5),eprop(6), 0d0,0d0,0d0/)
         stm = 8d0*eprop(7)*beta(1)
         sts = 2d0/9d0*(eprop(8)+eprop(9))*beta(2)
         plast = .FALSE.
       ! ------------------------------------------------------------------------------------
       CASE (6)                        !Hyperelastic isotropic rubber            ( mtype == 6)
         eprop(1:12) = mat%prope(7:18)
         elast = .TRUE.
         plast = .FALSE.                   !consider plasticity ?
       ! ------------------------------------------------------------------------------------
       END SELECT

       !************************** LAYERED SOLID *************************************************
     ELSE  !nlayr >= 1      layered solid (at integration point level)
       thick = sec%rprop(1)             !original thickness
       minstr= sec%rprop(4)             !strain threshold to use TTTI

       nl=1
       elast = sec%iprop(3) == 0               !elastic problem
       plast = .NOT.elast                      !consider plasticity ?
       dm = sec%rprop(6:41)                 !linear elastic integrated matrix
       IF( plast )THEN  !plastic lamina
         IF( ALLOCATED ( rr ) )DEALLOCATE( thickl,zposl,rr,matsl,lvari )
         ALLOCATE( thickl(nlayr),zposl(nlayr),rr(5,nlayr),matsl(nlayr),lvari(nlayr) )
         i = 42                                !pointer to layer thickness
         CALL vecasi(nlayr,sec%rprop(i),thickl(1))        !layer thickness
         i = i+2*nlayr                         !pointer to layer z position
         !CALL vecasi(nlayr,sec%rprop(i),zposl(1))         !layer z position
         i = i+nlayr                           !pointer to layer rotation matrix
         CALL vecasi(nlayr*5,sec%rprop(i),rr(1,1))        !layer rotation matrix

         lvari(1:nlayr)= sec%iprop(nlayr+5:2*nlayr+4)
         DO il=1,nlayr
           mate= sec%iprop(4+il)
           mat => pmats(mate)%p        !search layer material
           matsl(il)%p => mat
         END DO
       END IF
       oldm = -1                              !recompute constants
     END IF
     newmt = .FALSE.                        !same material than previous ?
     IF( ALLOCATED(varin) ) DEALLOCATE (varin)
     IF( plast) ALLOCATE( varin(nvar,ngaus) )  !

   END IF !newmt

   !*** computes actual configuration
   x(:,1:nnode) = coora(:,e%lnods(1:nnode))               !coordinates
   u(:,1:nnode) = x(:,1:nnode) - coori(:,e%lnods(1:nnode))  !incremental displacements
   cq = 0d0  ! initializes average in-plane metric tensor at each face
   ! compute EAS parameter from incremental displacements
   e%alpha = e%alpha - DOT_PRODUCT( e%h, RESHAPE(u,(/ ndofe /)))/e%ka  !update EAS parameter
   ! compute variables associated with in-plane behavior
   DO ii=1,2 ! for each face
     i = 4*ii-3         !first node of the face (1 or 5)
     DO jj=1,4  !for each mid side point
       tq(:,1:2,jj,ii) = MATMUL(x(:,i:i+3),e%cdq(1:4,1:2,jj,ii))
       ! add to average metric tensor
       cq(1,ii) = cq(1,ii) + DOT_PRODUCT(tq(:,1,jj,ii),tq(:,1,jj,ii))*e%area(jj,ii) !C11
       cq(2,ii) = cq(2,ii) + DOT_PRODUCT(tq(:,2,jj,ii),tq(:,2,jj,ii))*e%area(jj,ii) !C22
       eh(jj,ii) =           DOT_PRODUCT(tq(:,1,jj,ii),tq(:,2,jj,ii))               !C12
       cq(3,ii) = cq(3,ii) + eh(jj,ii)*e%area(jj,ii)                                !C12
     END DO
     eh(:,ii) = eh(:,ii) - cq(3,ii)
     !compute transvere shear strain components
     tas(:,:) = MATMUL(x,e%nfdas(:,:,ii))  !der x/x_3  at the 4 sides
     IF( ii == 1 )THEN
       gas(:,1) = (x(:,4) -x(:,1))/2d0 !point A der x/eta
       gas(:,2) = (x(:,2) -x(:,1))/2d0 !point B der x/xita
       gas(:,3) = (x(:,3) -x(:,2))/2d0 !point C der x/eta
       gas(:,4) = (x(:,3) -x(:,4))/2d0 !point D der x/xita
       CALL bsma04(gas,tas,e%nfdas(:,:,1),bs(:,:,:,1),e%jacin(:,:,1),1,bh(:,:,1))     !compute B_s matrix at sampling points
     ELSE
       gas(:,1) = (x(:,8) -x(:,5))/2d0 !point A der x/eta
       gas(:,2) = (x(:,6) -x(:,5))/2d0 !point B der x/xita
       gas(:,3) = (x(:,7) -x(:,6))/2d0 !point C der x/eta
       gas(:,4) = (x(:,7) -x(:,8))/2d0 !point D der x/xita
       CALL bsma04(gas,tas,e%nfdas(:,:,2),bs(:,:,:,2),e%jacin(:,:,2),2,bh(:,:,2))     !compute B_s matrix at sampling points
     END IF
     DO g=1,4  !for each assumed strain point compute x_tan . x_X_3
       va(g) = DOT_PRODUCT(tas(:,g),gas(:,g))
     END DO
     vas(3,ii) =(va(2)+va(4))/2d0                                    !C_xi3
     vas(4,ii) =(va(1)+va(3))/2d0                                    !C_eta3
     vas(1,ii) = e%jacin(1,1,ii)*vas(3,ii)+e%jacin(2,1,ii)*vas(4,ii) !x_1-x_3
     vas(2,ii) = e%jacin(1,2,ii)*vas(3,ii)+e%jacin(2,2,ii)*vas(4,ii) !x_2-x_3
     vas(3,ii) =-va(2)+va(4)                                         !C_xi3_H
     vas(4,ii) =-va(1)+va(3)                                         !C_eta3_H
   END DO

   CALL bmat04q(e%cdq,bq,tq,bm,e%area) !in-plane b-matrix at faces and stabilization
   ! EAS
   f3 = MATMUL(x(:,1:nnode),e%cartd)     !f_3 at element center
   c33 = DOT_PRODUCT(f3,f3)              !C(3,3) at element center
   DO i=1,nnode
     b3(:,i) = f3*e%cartd(i)           !b(:,3) matrix at element center
   END DO
   !   check
   flag2 = .FALSE.    !initializes to elastic step
   IF( ASSOCIATED(e%gausv) )THEN  !if internal variables already exist
     varin = e%gausv                !pass to auxiliar array
   ELSE IF( plast ) THEN
     varin = 0d0                    !set internal variables to zero
   END IF
   !   Gauss point loop
   ! initializes variables to be computed over GP loop
   ka = 0d0                 !factor Ka
   ralpha = 0d0             !EAS residual
   seq = 0d0                !initializes integrated equivalent stresses
   deq = 0d0                !initializes integrated equivalent D3 vector
   DO g=1,ngaus      !for each Gauss point
     fac = EXP(2d0*e%alpha*thf(g))                             !
     voli = DOT_PRODUCT(sh(:,g),e%dvol)*wei(g)                 !gauss point volume
     !interpolate metric tensor from lower and upper surfaces
     stran((/1,2,4/)) = shape(1,g)*cq(:,1) + shape(2,g)*cq(:,2) !  in-plane components
     stran(5:6) = shape(1,g)*vas(1:2,1) + shape(2,g)*vas(1:2,2) !transverse shear components
     c33z = c33*fac                                             ! out-of-plane components
     stran(3) = c33z                                            !U^2(3,3)
     IF( small )THEN
       flag = 2                   ! no spectral decomposition
     ELSE
       flag = 1                   ! initializes to no spectral decomposition
     END IF
     elen = voli**0.333d0           ! ELEMENT FRACTURE LENGHT (initial)
     !************************** STANDARD SOLID *****************************************
     IF( nlayr == 0 )THEN            !standard solid section (not layered)
       ! ------------------------------------------------------------------------------------
       SELECT CASE (mtype)
       ! ------------------------------------------------------------------------------------
       CASE (1)                        !Isotropic Elastic - Anisotropic Plastic  ( mtype == 1)
         IF( small )THEN               !Green Lagrange strains (twice)
           delta = (SUM(stran(1:3))-3d0)/6d0       !volumetric strain /3
           stran(1:3) = stran(1:3)-delta*2d0-1d0   !shear strains (twice)
         ELSE   ! Hencky (logarithmic) strains (volumetric and deviatoric parts)
           ! stran = twice the deviatoric strains    Delta = Ln(J)/3
           CALL lgst18(stran,r,lb,'RESVPL',ierr,flag,delta)
           IF( ierr == 1 )THEN
             WRITE(55,"(15i5)",ERR=9999) e%lnods(1:nnode)
             WRITE(55,"(6e12.4)",ERR=9999) stran(1:6)
             WRITE(55,"(3e15.4)",ERR=9999) x
             RETURN !CALL runen3('RESVPL: negative eigenvalues of U^2')
           END IF
         END IF
         !elastic (trial) strains
         IF( .NOT.elast )THEN
           !IF( ASSOCIATED(e%gausv)) e%gausv(1:7,g) = e%gausv(8:14,g)       !last converged
           strpl(1:6) = varin(1:6,g)            !previous (twice) plastic strains
           stran(1:6) = stran(1:6) - strpl(1:6)   !trial Elastic log strains
         END IF
         str = gm*stran                         !Trial elastic shear stresses
         !IF( plast ) THEN              !Consider plasticity
         !  efpst = varin(7,g)        !effect plastic strain
         !  CALL corr18(str(1),str(2),str(3),str(4),str(5),str(6), &
         !              efpst,gm,props,fr,ierr,dstpl,pflag,is,numpt,val)
         !  IF(ierr == 1) RETURN              !no convergence in plasticity
         !  IF( pflag )THEN                      !if plastic flow
         !    varin(1:6,g) = varin(1:6,g) + dstpl(1:6)  !total plastic shear strains
         !    varin(7,g) = varin(7,g) + efpst           !Total effect. plastic strain
         !    flag2 = .TRUE.
         !  END IF
         !END IF
         str(1:3) = str(1:3) + delta*km      !add press to deviatoric stress tensor
       ! ------------------------------------------------------------------------------------

       CASE (5)                        !Orthotropic Elastic - No Plasticity      ( mtype == 5)
         IF( small )THEN               !Green Lagrange strains (twice)
           stran(1:3) = (stran(1:3)-1d0)/2d0   !2nd Piola Kirchhoff stress
         ELSE   ! Hencky (logarithmic) strains (volumetric and deviatoric parts)
           ! stran = twice the deviatoric strains    Delta = Ln(J)/3
           CALL lgst18(stran,r,lb,'RESVPL',ierr,flag,delta)
           stran(1:3) = stran(1:3)/2 + delta
         END IF
         str(1) = eprop(1)*stran(1)+eprop(2)*stran(2)+eprop(3)*stran(3)
         str(2) = eprop(2)*stran(1)+eprop(4)*stran(2)+eprop(5)*stran(3)
         str(3) = eprop(3)*stran(1)+eprop(5)*stran(2)+eprop(6)*stran(3)
         str(4) = eprop(7)*stran(4)
         str(5) = eprop(8)*stran(5)
         str(6) = eprop(9)*stran(6)
       ! ------------------------------------------------------------------------------------
       CASE (6)                        !Hyperelastic isotropic rubber            ( mtype == 6)
         CALL rubber3d(eprop,stran,mat%matdef(8),stre=str,d3=d3)
         !CALL eige18(stran,r,lb,flag)          !squared eigenvalues
         !CALL rubber3dn(eprop,lb,mat%matdef(8),str)   !compute principal stresses and pressure
       ! ------------------------------------------------------------------------------------
       END SELECT

     !************************** LAYERED SOLID *************************************************
     ELSE  !nlayr >= 1      layered solid (at integration point level)

       ! check if laminate integration is necessary
       IF (elast) THEN
         ttti = .FALSE.
       ELSE
         aux  = ABS(stran(1)-1d0)+ABS(stran(2)-1d0)+ABS(stran(3)-1d0)+ &
                ABS(stran(4))+ABS(stran(5))+ABS(stran(6))
         ttti = aux  > minstr   !laminate integration?
       END IF
       pflag = .FALSE.                    !initializes flag

       IF( ttti )THEN
         IF( small )THEN               !Green Lagrange strains (twice)
           stran(1:3) = (stran(1:3)-1d0)*0.5   !shear strains (twice)
         ELSE   ! Hencky (logarithmic) strains (volumetric and deviatoric parts)
           !computes Hencky (logarithmic) strains (volumetric and deviatoric parts)
           ! stran = twice the deviatoric strains    Delta = Ln(J)/3
           CALL lgst18(stran,r,lb,'RESVPL',ierr,flag,delta)
           IF( ierr == 1 )THEN
             WRITE(55,"(8i5)",ERR=9999) e%lnods(1:nnode)
             WRITE(55,"(6e12.4)",ERR=9999) stran(1:6)
             WRITE(55,"(3e15.4)",ERR=9999) x
             RETURN !CALL runen3('RESVPL: negative eigenvalues of U^2')
           END IF
           stran(1:3) = (stran(1:3)/2d0+delta)   !recompute axial strains
         END IF
         str = 0d0                         !Initialization
         !Trought laminate integration loop (rule of mixtures)
         DO il=1,nlayr                        !for each layer
           !Rotate strains from element basis to layer basis (if necessary)
           IF( rr(1,il) < 0.9999999999 )THEN  ! Angle is not zero
             stral(1) = rr(1,il)*stran(1)+ rr(2,il)*stran(2)+ rr(3,il)*stran(4) !e11
             stral(2) = rr(2,il)*stran(1)+ rr(1,il)*stran(2)- rr(3,il)*stran(4) !e22
             stral(3) = stran(3)                                                !e33
             stral(4) =-rr(4,il)*stran(1)+ rr(4,il)*stran(2)+ rr(5,il)*stran(4) !e12
             stral(5) = stran(5)                                                !e13
             stral(6) = stran(6)                                                !e23
           ELSE
             stral(1:6) = stran(1:6)
           END IF

           IF (il==nlayr) THEN
             ilv=lvari(il)
             jlv=nvar
           ELSE
             ilv=lvari(il)
             jlv=lvari(il+1)-1
           END IF

           mat => matsl(il)%p        ! pointer to layer material

           SELECT CASE (mat%mtype)
           ! ------------------------------------------------------------------------------------
           CASE (5)                        !Orthotropic Elastic - No Plasticity      ( mtype == 5)
             strel(1) = eprop(1)*stral(1)+eprop(2)*stral(2)+eprop(3)*stral(3)
             strel(2) = eprop(2)*stral(1)+eprop(4)*stral(2)+eprop(5)*stral(3)
             strel(3) = eprop(3)*stral(1)+eprop(5)*stral(2)+eprop(6)*stral(3)
             strel(4) = eprop(7)*stral(4)
             strel(5) = eprop(8)*stral(5)
             strel(6) = eprop(9)*stral(6)
           ! ------------------------------------------------------------------------------------
           END SELECT

           !Rotate stresses from layer basis to element basis (if necessary)
           IF( rr(1,il) < 0.9999999999 )THEN  ! Angle is not zero
             staux(1) = strel(1)*rr(1,il)+ strel(2)*rr(2,il)- strel(4)*rr(4,il)
             staux(2) = strel(1)*rr(2,il)+ strel(2)*rr(1,il)+ strel(4)*rr(4,il)
             staux(3) = strel(3)
             staux(4) = strel(1)*rr(3,il)- strel(2)*rr(3,il)+ strel(4)*rr(5,il)
             staux(5:6) = strel(5:6)
           ELSE
             staux(1:6) = strel(1:6)
           END IF

           !***   compute laminate stress on integration point level
           str(1:6) = str(1:6) + staux(1:6)*thickl(il)/thick   ! laminate integrated stresses
         END DO !l (loop to next layer)

       ELSE !elastic response of integrated laminated solid (small strains only)
         stran(1:3) = (stran(1:3) - 1d0 )*0.5   !2nd Piola Kirchhoff stress
         str(1:6) = MATMUL(RESHAPE((dm),(/6,6/)),stran(1:6))
       END IF

     END IF  !nlayr == 0

     IF(mtype == 6)THEN  !rubbers
       sig = str
       !!Second Piola Kirchhoff stress tensor
       !DO i=1,3
       ! str(i) = (str(i)+str(4)/lb(i))/lb(i)     ! S = [ Phid_i + (Li Phiv_i)/Li ]/Li
       !END DO
       !! store the stress vector (2nd Piola-Kirchhoff stress tensor)
       !!Compute 3-D stresses with the spectral decomposition  R^T * Beta * R
       !sig(1) = r(1,1)*str(1)*r(1,1) + r(1,2)*str(2)*r(1,2) + r(1,3)*str(3)*r(1,3) !(1,1)
       !sig(2) = r(2,1)*str(1)*r(2,1) + r(2,2)*str(2)*r(2,2) + r(2,3)*str(3)*r(2,3) !(2,2)
       !sig(3) = r(3,1)*str(1)*r(3,1) + r(3,2)*str(2)*r(3,2) + r(3,3)*str(3)*r(3,3) !(3,3)
       !sig(4) = r(1,1)*str(1)*r(2,1) + r(1,2)*str(2)*r(2,2) + r(1,3)*str(3)*r(2,3) !(1,2)
       !sig(5) = r(1,1)*str(1)*r(3,1) + r(1,2)*str(2)*r(3,2) + r(1,3)*str(3)*r(3,3) !(1,3)
       !sig(6) = r(2,1)*str(1)*r(3,1) + r(2,2)*str(2)*r(3,2) + r(2,3)*str(3)*r(3,3) !(2,3)
       e%stint(:,g) = sig ! keep 2nd Piola Kirchhoff
     ELSE
       !e%stint(:,g) = str ! keep Kirchhoff stress for output
       IF( flag == 0)THEN
         ! Computes Hencky stress on the natural Frame T(L) = R^T T R
         sig(1) = r(1,1)*str(1)*r(1,1)+r(2,1)*str(2)*r(2,1)+r(3,1)*str(3)*r(3,1)+ &
             2d0*(r(1,1)*str(4)*r(2,1)+r(1,1)*str(5)*r(3,1)+r(2,1)*str(6)*r(3,1))
         sig(2) = r(1,2)*str(1)*r(1,2)+r(2,2)*str(2)*r(2,2)+r(3,2)*str(3)*r(3,2)+ &
             2d0*(r(1,2)*str(4)*r(2,2)+r(1,2)*str(5)*r(3,2)+r(2,2)*str(6)*r(3,2))
         sig(3) = r(1,3)*str(1)*r(1,3)+r(2,3)*str(2)*r(2,3)+r(3,3)*str(3)*r(3,3)+ &
             2d0*(r(1,3)*str(4)*r(2,3)+r(1,3)*str(5)*r(3,3)+r(2,3)*str(6)*r(3,3))
         sig(4) = r(1,1)*str(1)*r(1,2)+r(2,1)*str(2)*r(2,2)+r(3,1)*str(3)*r(3,2)+ &
                  r(1,1)*str(4)*r(2,2)+r(1,1)*str(5)*r(3,2)+r(2,1)*str(6)*r(3,2)+ &
                  r(2,1)*str(4)*r(1,2)+r(3,1)*str(5)*r(1,2)+r(3,1)*str(6)*r(2,2)
         sig(5) = r(1,1)*str(1)*r(1,3)+r(2,1)*str(2)*r(2,3)+r(3,1)*str(3)*r(3,3)+ &
                  r(1,1)*str(4)*r(2,3)+r(1,1)*str(5)*r(3,3)+r(2,1)*str(6)*r(3,3)+ &
                  r(2,1)*str(4)*r(1,3)+r(3,1)*str(5)*r(1,3)+r(3,1)*str(6)*r(2,3)
         sig(6) = r(1,2)*str(1)*r(1,3)+r(2,2)*str(2)*r(2,3)+r(3,2)*str(3)*r(3,3)+ &
                  r(1,2)*str(4)*r(2,3)+r(1,2)*str(5)*r(3,3)+r(2,2)*str(6)*r(3,3)+ &
                  r(2,2)*str(4)*r(1,3)+r(3,2)*str(5)*r(1,3)+r(3,2)*str(6)*r(2,3)
        ! Computes 2nd P-K stress on the natural Frame S(L) = F(T(L)
         str(1) = sig(1)/lb(1)**2
         str(2) = sig(2)/lb(2)**2
         str(3) = sig(3)/lb(3)**2
         IF( ABS(lb(1)-lb(2)) > 1.d-6)THEN   !lb(1) /= lb(2)
           str(4) = sig(4)*2d0*LOG(lb(1)/lb(2))/(lb(1)**2-lb(2)**2)
         ELSE                                !lb(1) = lb(2)
           str(4) = sig(4)/lb(1)/lb(2)
         END IF
         IF( ABS(lb(1)-lb(3)) > 1.d-6)THEN   !lb(1) /= lb(3)
           str(5) = sig(5)*2d0*LOG(lb(1)/lb(3))/(lb(1)**2-lb(3)**2)
         ELSE                                !lb(1) = lb(3)
           str(5) = sig(5)/lb(1)/lb(3)
         END IF
         IF( ABS(lb(2)-lb(3)) > 1.d-6)THEN   !lb(2) /= lb(3)
           str(6) = sig(6)*2d0*LOG(lb(2)/lb(3))/(lb(2)**2-lb(3)**2)
         ELSE                                !lb(2) = lb(3)
           str(6) = sig(6)/lb(2)/lb(3)
         END IF
        ! Computes 2nd P-K on the Lagrangian Frame S = R S(L) R^T
         sig(1) = r(1,1)*str(1)*r(1,1)+r(1,2)*str(2)*r(1,2)+r(1,3)*str(3)*r(1,3)+ &
             2d0*(r(1,1)*str(4)*r(1,2)+r(1,1)*str(5)*r(1,3)+r(1,2)*str(6)*r(1,3))
         sig(2) = r(2,1)*str(1)*r(2,1)+r(2,2)*str(2)*r(2,2)+r(2,3)*str(3)*r(2,3)+ &
             2d0*(r(2,1)*str(4)*r(2,2)+r(2,1)*str(5)*r(2,3)+r(2,2)*str(6)*r(2,3))
         sig(3) = r(3,1)*str(1)*r(3,1)+r(3,2)*str(2)*r(3,2)+r(3,3)*str(3)*r(3,3)+ &
             2d0*(r(3,1)*str(4)*r(3,2)+r(3,1)*str(5)*r(3,3)+r(3,2)*str(6)*r(3,3))
         sig(4) = r(1,1)*str(1)*r(2,1)+r(1,2)*str(2)*r(2,2)+r(1,3)*str(3)*r(2,3)+ &
                  r(1,1)*str(4)*r(2,2)+r(1,1)*str(5)*r(2,3)+r(1,2)*str(6)*r(2,3)+ &
                  r(1,2)*str(4)*r(2,1)+r(1,3)*str(5)*r(2,1)+r(1,3)*str(6)*r(2,2)
         sig(5) = r(1,1)*str(1)*r(3,1)+r(1,2)*str(2)*r(3,2)+r(1,3)*str(3)*r(3,3)+ &
                  r(1,1)*str(4)*r(3,2)+r(1,1)*str(5)*r(3,3)+r(1,2)*str(6)*r(3,3)+ &
                  r(1,2)*str(4)*r(3,1)+r(1,3)*str(5)*r(3,1)+r(1,3)*str(6)*r(3,2)
         sig(6) = r(2,1)*str(1)*r(3,1)+r(2,2)*str(2)*r(3,2)+r(2,3)*str(3)*r(3,3)+ &
                  r(2,1)*str(4)*r(3,2)+r(2,1)*str(5)*r(3,3)+r(2,2)*str(6)*r(3,3)+ &
                  r(2,2)*str(4)*r(3,1)+r(2,3)*str(5)*r(3,1)+r(2,3)*str(6)*r(3,2)
       ELSE IF(flag == 1)THEN  !all eigenvalues are almost equal
         sig(:) = str(:)/lb(1) !lambda squared
       ELSE
         sig = str
       END IF
       e%stint(:,g) = sig ! keep 2nd P-Kirchhoff stress for output
     END IF

     !***   compute Int(B**t*sigma) on element level

     sig = sig*voli          !Original dvol used (TLF)

     ! Compute equivalent stresses and constitutive matrix
     seq( 1: 3) = seq( 1: 3) + sig((/1,2,4/))*shape(1,g)
     seq( 4: 6) = seq( 4: 6) + sig((/1,2,4/))*shape(2,g)
     seq( 7: 8) = seq( 7: 8) + sig(5:6)      *shape(1,g)
     seq( 9:10) = seq( 9:10) + sig(5:6)      *shape(2,g)
     seq(11)    = seq(11)    + sig(3)        *fac
     staux = d3*c33z*thf(g)*voli             !D3*C33*zeta*Dvol
     deq( 1: 3) = deq( 1: 3) + staux((/1,2,4/))*shape(1,g)
     deq( 4: 6) = deq( 4: 6) + staux((/1,2,4/))*shape(2,g)
     deq( 7: 8) = deq( 7: 8) + staux(5:6)      *shape(1,g)
     deq( 9:10) = deq( 9:10) + staux(5:6)      *shape(2,g)
     deq(11)    = deq(11)    + (staux(3) + sig(3)*2d0*thf(g))*fac   !why fac
     ! compute  EAS residual and stiffness
     ralpha = ralpha + sig(3)*thf(g)*c33z
     ka = ka + (staux(3) + sig(3)*2d0*thf(g))*c33z *thf(g)

   END DO
   DO i=1,4 !for each face node
     j  = i+4  !for each other face node
     rl(:,i) = seq( 1)*bq(:,i,1,1) + seq( 2)*bq(:,i,2,1) + seq( 3)*bq(:,i,3,1) + &
               seq( 7)*bs(:,i,1,1) + seq( 8)*bs(:,i,2,1) +                       &
               seq( 9)*bs(:,i,1,2) + seq(10)*bs(:,i,2,2) + seq(11)*b3(:,i)
     rl(:,j) = seq( 4)*bq(:,i,1,2) + seq( 5)*bq(:,i,2,2) + seq( 6)*bq(:,i,3,2) + &
               seq( 7)*bs(:,j,1,1) + seq( 8)*bs(:,j,2,1) +                       &
               seq( 9)*bs(:,j,1,2) + seq(10)*bs(:,j,2,2) + seq(11)*b3(:,j)
     h (:,i) = deq( 1)*bq(:,i,1,1) + deq( 2)*bq(:,i,2,1) + deq( 3)*bq(:,i,3,1) + &
               deq( 7)*bs(:,i,1,1) + deq( 8)*bs(:,i,2,1) +                       &
               deq( 9)*bs(:,i,1,2) + deq(10)*bs(:,i,2,2) + deq(11)*b3(:,i)
     h (:,j) = deq( 4)*bq(:,i,1,2) + deq( 5)*bq(:,i,2,2) + deq( 6)*bq(:,i,3,2) + &
               deq( 7)*bs(:,j,1,1) + deq( 8)*bs(:,j,2,1) +                       &
               deq( 9)*bs(:,j,1,2) + deq(10)*bs(:,j,2,2) + deq(11)*b3(:,j)
   END DO
   e%ka = ka                        !keep EAS stiffness
   e%h  = RESHAPE( h, (/ndofe/))    !keep EAS vector
   rl   = rl - h*ralpha/ka          !modify residual forces using EAS residual and vector
   e%alpha = e%alpha - ralpha/e%ka  !first update of EAS parameter
   e%se(1:11) = seq                       !keep integrated stresses
   !Stabilization
   IF( mtype == 6 )THEN
      stm = d3(3)*beta(1)
      sts = d3(3)*beta(2)
   END IF
   ! compute in-plane stabilization
   ka = e%dvol(2)*stm ! jacobian at element center times Beta_m x 8G
   DO i=1,2  !for each face
     DO j=1,4   !for each mid-side point
       eh(j,i) = eh(j,i)*ka*e%area(j,i) !S_12^KH
     END DO
   END DO
   e%se(12:19) = RESHAPE( eh, (/8/)) !keep integrated stabilization forces
   CALL stbm04r(rl,bm,eh)
   ! compute shear stabilization
   ka = e%dvol(2)*sts
   vas(3:4,:) = vas(3:4,:)*ka                                         !C_xi3_H
   CALL stbs04r(rl,bh,vas(3:4,:),e%jacin)
   DO i=1,nnode             !for each node in the element
     n = e%lnods(i)     !node
     resid(1:3,n) = resid(1:3,n)+rl(1:3,i) !sum internal force
   END DO

   IF( flag2 )THEN         !if there are variables to modify
     IF(.NOT.ASSOCIATED(e%gausv))   ALLOCATE( e%gausv(nvar,ngaus))
     e%gausv = varin
   END IF
   IF( ielem == iel )THEN
     WRITE(58,"(' Fuerzas nodales equivalentes para el elemento :',i5)")ielem
     WRITE(58,"(3e15.5 )")rl(:,:)
    END IF
   e => e%next          !point to next element in the list
 END DO
 IF(ALLOCATED(varin)) DEALLOCATE(varin)
 !resid = 0d0
RETURN
 9999 CALL runen2('')
 END SUBROUTINE resv04


 SUBROUTINE stbm04r(s,b,eh)
 IMPLICIT NONE
 ! dummy arguments
 REAL(kind=8), INTENT(IN) :: eh(:,:),b(:,:,:)
 REAL(kind=8), INTENT(IN OUT) :: s(:,:)
 ! local variables
 INTEGER(kind=4) :: j,k,l,m,i0,i
 REAL(kind=8) :: f

 i0 = 1   !first position in r for first face
 DO m=1,2  !for each face
   DO k=1,4        !for each mid-side point
     l = 0         !initializes position in B mat
     f = eh(k,m)     !integrated stabilization force
     DO i=i0,i0+3    !for each node of the face
       DO j=1,3        !for each DOF in the face-node
         l = l+1         !row
         s(j,i) = s(j,i) + f*b(l,k,m)
       END DO
     END DO
   END DO
   i0 = 5        !first position in r for second face
 END DO

 RETURN

 END SUBROUTINE stbm04r


 SUBROUTINE stbs04r(s,bs,eh,ji)
 IMPLICIT NONE
 ! dummy arguments
 REAL(kind=8), INTENT(IN) :: eh(:,:),bs(:,:,:),ji(:,:,:)
 REAL(kind=8), INTENT(IN OUT) :: s(:,:)
 ! local variables
 INTEGER(kind=4) :: i,j,l,m
 REAL(kind=8) :: a,b

 DO m=1,2  !for each face
   a = (ji(1,1,m)*ji(1,1,m) + ji(2,1,m)*ji(2,1,m))*eh(1,m)
   b = (ji(1,2,m)*ji(1,2,m) + ji(2,2,m)*ji(2,2,m))*eh(2,m)
   l = 0         !initializes position in B mat
   DO i=1,8       !for each node
     DO j=1,3       !for each DOF
       l = l+1    !row
       s(j,i) = s(j,i) + a*bs(l,1,m) + b*bs(l,2,m)
     END DO
   END DO
 END DO

 RETURN

 END SUBROUTINE stbs04r
