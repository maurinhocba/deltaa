 SUBROUTINE stif12p(heade, gstif, force, coora, quad, nnode, zigzag)

 ! 6-node solid-shell element (Sprism).
 ! use Green-Lagrange strains and 2nd Piola-Kirchhoff stresses
 USE npo_db, ONLY : psia,coord
 IMPLICIT NONE
 ! dummy arguments
 INTEGER (kind=4), INTENT(IN) ::  nnode    !number of nodes in the patch
 LOGICAL, INTENT(IN) ::  quad,    &  ! use quadratic approac for in-plane metric tensor
                         zigzag
 REAL (kind=8), INTENT(IN) :: coora(:,:)     ! present coordinates
 REAL (kind=8), INTENT(IN OUT) :: gstif(:),force(:)  !global stiffness and force vector
 TYPE (ele12), POINTER :: heade   !pointer to first element

 !Local Variables
 INTEGER (kind=4), PARAMETER :: ndime =3

 TYPE (ele12), POINTER :: e               !pointer to an element
 LOGICAL  :: newmt,   &  !if present material different than previous
             sides(3)    !if side element exists (quad approach)
 INTEGER (kind=4) :: i,j,ii,jj,       &  !different indexes
                     isec,osec,       &  !present and old section
                     nst,naux,        &  !
                     nvarl,ielem,nnf     !number of DOFs

 REAL (kind=8) :: d(0:109),             & !(nnn) material properties
                  thick

 REAL (kind=8) f3(3),c33,b3(3,6), & !gradient 3, C33 and associate B matrix
               area                 !element area, nodal bsf matrix

 REAL (kind=8) :: x(ndime,nnode),    &   !local coordinates
                  di(153),daux(153)             !in-plane metric tensor at faces, Auxiliar D matrix
 REAL (kind=8), ALLOCATABLE ::  bmatx(:,:),  &  !global B matrix (to be interpolated)
                                bq(:,:,:,:), &  !(ndime,nnb,3,2)  !in-plane B matrix at faces
                                tq(:,:,:,:), &  !(ndime,2,3,2)
                                stiff(:)        !stiffness matrix
 ! variables for assumed strain transverse shear
 REAL (kind=8) :: tas(3,3),      & ! der x/x_3 at Assumed Strain points
                  gas(3,3),      & ! der x/tan at Assumed Strain points
                  bs(3,6,2,2),   & ! Bs matrix at faces
                  btp(2,3,2),bpp(2,3,4), & ! Bt_phi and Bp_phi matrix at center
                  cd(3,2)          ! basic triangle derivatives


 TYPE (section), POINTER :: sec   !pointer to section

 d(0) = 0d0           !give a null value of many terms
 nvarl = ndime*nnode  !nnode*ndime number of basic element DOFs
 nst   = 11           !number of equivalent stress/strain measures
 naux  = 66           !nst*(nst+1)/2 size of constitutive matrix
 nnf = 3
 IF(zigzag) THEN
   nvarl = nvarl+2*nnf    !add additional DOFs
   nst   = nst + 4 + 2    !add additional strain measures
   naux  = 153            !nst*(nst+1)/2 resize consitutive matrix
 END IF
 ALLOCATE (stiff(nvarl*(nvarl+1)/2),bmatx(nvarl,nst))    !get memory for element stiffness matrix (upper part)
 IF( quad )THEN  !get memory for gradients at faces and associated B matrices
    ALLOCATE (  tq(ndime,2,3,2), bq(ndime,6,3,2) )       !quad approach
 ELSE
    ALLOCATE (  tq(ndime,2,1,2), bq(ndime,3,3,2) )       !standard approach
 END IF

 !***  Compute Element Stiffness matrix for all elements
 bmatx = 0d0           !initializes B-matrix
 osec = -1             !initialize to a non-existent section
 e => heade            !point to first element

 DO
   IF (.NOT.ASSOCIATED (e) ) EXIT    !last element processed => EXIT
   isec = e%matno                    !associated section to element
   ielem = e%numel
   newmt = osec /= isec              !a different material must be considered
   IF(newmt)THEN                     !if new material, get properties
     osec = isec                     !keep present material
     sec => psecs(isec)%p            !point to section
     thick = sec%rprop(1)
     d(1:109)     = sec%rprop(6:114)
     IF( zigzag )THEN
       di(  1: 17) = d((/ 1:4,24:26,0,0,0,0,50:53,0,0 /))    ! N1
       di( 18: 33) = d((/ 5:7,28:30,0,0,0,0,54:57,0,0 /))    ! N2
       di( 34: 48) = d((/ 8:9,32:34,0,0,0,0,58:61,0,0 /))    ! N12
       di( 49: 62) = d((/  10,36:38,0,0,0,0,62:65,0,0 /))    ! N3
       di( 63: 75) = d((/     11:13,0,0,0,0,66:69,0,0 /))    ! M1
       di( 76: 87) = d((/     15:16,0,0,0,0,70:73,0,0 /))    ! M2
       di( 88: 98) = d((/        18,0,0,0,0,74:77,0,0 /))    ! M12
       !       27,31,35,39,14,17,19,20, 0x4,78:81,0,0        ! M33
       !IF( sec%iprop(4) == 1 )THEN ! For trigonometric use computed shear factor
       !  IF( sec%rprop(4) == 0d0 )THEN !use computed shear factor
       !    d((/89,92,95,99,102,106/)) = d((/89,92,95,99,102,106/)) *sec%rprop(115)
       !    d((/90,93,96,97,100,103,104,107,108/)) = d((/90,93,96,97,100,103,104,107,108/))*sec%rprop(116)
       !    d((/91,94,98,101,105,109/)) = d((/91,94,98,101,105,109/)) *sec%rprop(117)
       !  ELSE                          !use provided shear factor
       !    d(89:108) = d(89:108)*sec%rprop(4)
       !  END IF
       !END IF
       di( 99:108) = d((/ 89,90,95,96,0,0,0,0,102,103 /))    ! S11
       di(109:117) = d((/    91,97,98,0,0,0,0,104,105 /))    ! S21
       di(118:125) = d((/       92,93,0,0,0,0,106,107 /))    ! S12
       di(126:132) = d((/          94,0,0,0,0,108,109 /))    ! S22
       di(133:138) = d((/                   40:43,0,0 /))    ! MF1
       di(139:143) = d((/                   44:46,0,0 /))    ! MF2
       di(144:147) = d((/                   47:48,0,0 /))    ! MF12
       di(148:150) = d((/                      49,0,0 /))    ! MF21
       di(151:152) = d((/                      99:100 /))    ! SF1
       di(    153) = d(                           101   )    ! SF2
     ELSE
       di(  1: 11) = d((/ 1:4,24:26,0,0,0,0 /))   !N11
       di( 12: 21) = d((/ 5:7,28:30,0,0,0,0 /))   !N22
       di( 22: 30) = d((/ 8:9,32:34,0,0,0,0 /))   !N12
       di( 31: 38) = d((/  10,36:38,0,0,0,0 /))   !N33
       di( 39: 45) = d((/     11:13,0,0,0,0 /))   !M11
       di( 46: 51) = d((/     15:16,0,0,0,0 /))   !M22
       di( 52: 56) = d((/        18,0,0,0,0 /))   !M12
       !     27,31,35,39,14,17,19,20,0x4        ! M33
       di( 57: 60) = d((/     89,90,95,96 /))   !!11
       di( 61: 63) = d((/        91,97,98 /))   !S21
       di( 64: 65) = d((/           92,93 /))   !S12
       di(     66) = d(                94   )   !S22
       IF( sec%rprop(4) == 0d0 )THEN !use computed shear factor
         di((/57,59,64/)) = di((/57,59,64/))*sec%rprop(115)
         di((/58,60,62,65/)) = di((/58,60,62,65/))*sec%rprop(116)
         di((/61,63,66/)) = di((/61,63,66/))*sec%rprop(117)
       ELSE                          !use provided shear factor
         di(57:66) = di(57:66)*sec%rprop(4)
       END IF
     END IF
     newmt = .FALSE.               !same material than previous ?

   END IF !newmt
   ! new configuration
   area = e%dvol/thick    !area
   x(:,1:6) = coora(:,e%lnods(1:6))     !nodes of the element
   IF( quad )THEN            !for quad approach get extra-nodes coordinates
     DO i=1,nnf
       sides(i) = e%lnods(i+6) /= 0
       IF( sides(i) )THEN   !if side element exists
         x(:,i+6:i+9:3) = coora(:,e%lnods(i+6:i+9:3))  !get extra nodes coordinates
       ELSE                              !else
         x(:,i+6:i+9:3) = 0d0                          !nullify
       END IF
     END DO
   END IF
   ! compute in-plane gradient at each mid-side point of upper and lower faces
   DO ii=1,2 ! for each face
     i = 1+nnf*(ii-1)     !first node of the face
     IF( quad )THEN  ! for quad approacu
       j = i+5         !position previous to extra node at each face 6 or 9
       DO jj=1,nnf  !for each mid side point
         tq(:,1:2,jj,ii) = MATMUL(x(:,i:i+2),e%cdq(1:nnf,1:2,jj,ii))  !main element contribution
         IF( sides(jj) )THEN                     !add extra node contribution if exist
           tq(:,1,jj,ii) = tq(:,1,jj,ii) + x(:,j+jj)*e%cdq(4,1,jj,ii)
           tq(:,2,jj,ii) = tq(:,2,jj,ii) + x(:,j+jj)*e%cdq(4,2,jj,ii)
         END IF
       END DO
     ELSE
       tq(:,1:2,1,ii) = MATMUL(x(:,i:i+2),e%cdq(1:3,1:2,1,ii))  !main element contribution
     END IF
     !compute transvere shear strain components
     tas(:,:) = MATMUL(x(:,1:6),e%nfdas(:,:,ii))  !der x/x_3 at the 3 mid-side points
     IF( ii == 1 )THEN      !bottom face
       gas(:,1) = x(:,3) -x(:,2) !point 1 der x/tan SQR(2)
       gas(:,2) = x(:,1) -x(:,3) !point 2 der -x/eta
       gas(:,3) = x(:,2) -x(:,1) !point 3 der x/xita
       CALL bsma12(gas,tas,e%nfdas(:,:,1),bs(:,:,:,1),e%jacin(:,:,1),1)     !compute B_s matrix at sampling points
     ELSE                   !top face
       gas(:,1) = x(:,6) -x(:,5) !point 1 der x/tan SQR(2)
       gas(:,2) = x(:,4) -x(:,6) !point 2 der -x/eta
       gas(:,3) = x(:,5) -x(:,4) !point 3 der x/xita
       CALL bsma12(gas,tas,e%nfdas(:,:,2),bs(:,:,:,2),e%jacin(:,:,2),2)     !compute B_s matrix at sampling points
     END IF
   END DO

   CALL bmat12q(e%cdq,bq,tq,quad) !in-plane b-matrix at faces

   IF( zigzag )THEN   ! compute BS_Phi matrix at element center
     ! compute basic triangle derivavites
     cd(:,:) = e%cdq(1:3,:,1,1)
     IF( quad )THEN
       cd(1,1:2) = cd(1,1:2) - e%cdq(4,1:2,1,1)
       cd(2,1:2) = cd(2,1:2) + e%cdq(4,1:2,1,1)
       cd(3,1:2) = cd(3,1:2) + e%cdq(4,1:2,1,1)
     END IF
     CALL bphi12(area,cd,btp,bpp,e%nangl,e%jacin(:,:,1))
   END IF
   ! EAS
   f3 = MATMUL(x(:,1:6),e%cartd)      ! x_3 at element center
   c33 = DOT_PRODUCT(f3,f3)           ! C33 at element center
   DO i=1,6
     b3(:,i) = f3*e%cartd(i)          ! B33 at element center
   END DO

   stiff = 0d0                     !initializes stiffness matrix
   daux(1:naux) = di(1:naux)*area  !scale constitutive matrix

   ! Build complete B matrix (shell type Bm B3 Bb Bs Bf )
   CALL bmat12(bmatx,bq,b3,bs,thick,quad,zigzag,bpp,btp,nnf)        !
   CALL btdbpr(bmatx(1,1),daux(1),stiff(1),nvarl,nst)     ! B^T D B
   ! Geometric matrix for in plane components
   IF( quad )THEN  !quad approach
     CALL kgmm12q(e%cdq,RESHAPE(e%se(12:17),(/3,2/)),stiff,nvarl)
   ELSE            !standard approach
     CALL kgmm12(e%cdq,RESHAPE(e%se(12:17),(/3,2/)),stiff,nvarl)
   END IF
   !! Kg for transverse shear
   !CALL kgms12p(e%nfdas,RESHAPE(e%se(18:21),(/2,2/)),e%jacin,stiff,nvarl)
   !! Kg for transverse stress !(Correct? does not improve convergence.  ¿important?)
   !!CALL kgmt12p(e%cartd,e%se(22),stiff,nnode)
   ! I S   N E X T   C A L L   E S S E N T I A L?
   IF( zigzag ) CALL stbp12(stiff,d(82:84),quad,area,cd,e%nangl,e%jacin(:,:,1))
   ! condense EAS parameter
   CALL cond12(stiff,e%h,e%ka,nvarl)
   ! add to global stiffness
   CALL stiaux_sp(nnode,e%lnods,nvarl,zigzag,stiff(1),force(1),gstif(1) )

   e => e%next
 END DO
! IF(.NOT.ASSOCIATED (heade%next)) THEN !just one element
!   WRITE(58,"('       Stiffness Matrix')")
!   ii= 0
!   DO i=1,nvarl
!     jj = i
!     DO j=1,i-1
!       bmatx(j,1) = stiff(jj)
!       jj = jj+nvarl-j
!     END DO
!     DO j=i,nvarl
!       ii=ii+1
!       bmatx(j,1) = stiff(ii)
!     END DO
!     IF(zigzag)bmatx(i,2) = DOT_PRODUCT(bmatx(:,1),dis)
!     WRITE(58,"(24e20.12)")bmatx(:,1)
!   END DO
!   IF(zigzag)THEN
!     WRITE(58,"('      product K u')")
!     WRITE(58,"(3e15.7)")bmatx(1:18,2)
!     WRITE(58,"(2e15.7)")bmatx(19:24,2)
!   END IF
! END IF
 DEALLOCATE( stiff,bmatx,bq,tq )
 RETURN

 END SUBROUTINE stif12p
 SUBROUTINE cond12(k,h,ka,n)
 IMPLICIT NONE
 ! dummy arguments
 INTEGER(kind=4), INTENT(IN) :: n
 REAL(kind=8), INTENT(IN) :: ka,h(:)
 REAL(kind=8), INTENT(IN OUT) :: k(:)
 ! local variables
 INTEGER(kind=4) :: i,j,l
 REAL(kind=8) :: f
 l = 0
 DO i=1,n
   f = h(i)/ka
   DO j=i,n
     l = l+1
     k(l) = k(l) - f*h(j)
   END DO
 END DO

 RETURN

 END SUBROUTINE cond12


 SUBROUTINE stbp12(k,d,quad,area,cd,nangl,ji)
 ! compute Stabilization matrix for add transverse shear
 !
 ! This is based on TLLL transverse shear stabilization?
 ! It seems to work, not sure it is essential
 ! no stabilization forces are added at resv12p
 !
 IMPLICIT NONE
 ! dummy arguments
 REAL(kind=8), INTENT(IN OUT) :: k(:)
 LOGICAL, INTENT(IN) :: quad
 REAL (kind=8), INTENT(IN) :: d(3),area,cd(3,2),ji(2,2),nangl(2,3)
 ! local variables
 INTEGER(kind=4) :: i,j,l
 REAL(kind=8) :: abc, b(2),bp(6)


 abc = d(1)*( ji(1,1)**2 + ji(2,1)**2 + ji(2,1)*ji(1,1) ) &
     + d(3)*( ji(1,2)**2 + ji(2,2)**2 + ji(2,2)*ji(1,2) ) &
     + d(2)*(2*ji(1,2)*ji(1,1) + 2*ji(2,2)*ji(2,1) + ji(2,2)*ji(1,1) + ji(2,1)*ji(1,2) )
 abc = abc*area/18d0

 l = 1
 DO i=1,3
   b(:) = (/ -cd(i,2),+cd(i,1) /)*area
   bp(l  ) = nangl(1,i)*b(1)+nangl(2,i)*b(2)  !transform to nodal system
   bp(l+1) =-nangl(2,i)*b(1)+nangl(1,i)*b(2)
   l = l+2
 END DO

 ! last position of first part in stiffness matrix
 l = 9*(19+12)
 IF( quad ) l = 18*(37+12)


 DO i=1,6
   DO j=i,6
     l = l+1
     k(l) = k(l) + abc*bp(i)*bp(j)
   END DO
 END DO

 RETURN

 END SUBROUTINE stbp12

! J^(-T)  D    J^(-1)
! 11 12  1 2   11  21
! 21 22  2 3   12  22
!
! J^(-T)  D =[1 11 + 2 12   2 11 + 3 12]
!            [1 21 + 2 22   2 21 + 3 22]
!
![1 11 11 + 2 12 11 + 2 11 12 + 3 12 12      1 11 21 + 2 12 21 + 2 11 22 + 3 12 22]
![1 21 11 + 2 22 11 + 2 21 12 + 3 22 12      1 21 21 + 2 22 21 + 2 21 22 + 3 22 22]
!
![d(1)*ji(1,1)*ji(1,1) +d(2)*ji(1,2)*ji(1,1) +d(2)*ji(1,1)*ji(1,2)+d(3)*ji(1,2)*ji(1,2)     d(1)*ji(1,1)*ji(2,1) +d(2)*ji(1,2)*ji(2,1) +d(2)*ji(1,1)*ji(2,2) +d(3)*ji(1,2)*ji(2,2) ]
![d(1)*ji(2,1)*ji(1,1) +d(2)*ji(2,2)*ji(1,1) +d(2)*ji(2,1)*ji(1,2)+d(3)*ji(2,2)*ji(1,2)     d(1)*ji(2,1)*ji(2,1) +d(2)*ji(2,2)*ji(2,1) +d(2)*ji(2,1)*ji(2,2) +d(3)*ji(2,2)*ji(2,2) ]
!
! a+b+c =
!
!


