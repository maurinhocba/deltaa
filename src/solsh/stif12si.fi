 SUBROUTINE stif12si(heade, gstif, ustif, force, coora, nnode, zigzag, beta)

 ! 8-node solid-shell element (SSOLAG).
 ! use Almansi strains and associated stresses

 USE npo_db, ONLY : psia,coord
 USE solv_db, ONLY : nsymm
 IMPLICIT NONE
 ! dummy arguments
 INTEGER (kind=4), INTENT(IN) :: nnode    !number of nodes

 LOGICAL, INTENT(IN) :: zigzag    ! use additional DOFs
 REAL (kind=8), INTENT(IN) :: coora(:,:), &  ! present coordinates
                              beta(2)        !stabilization factors
 REAL (kind=8), INTENT(IN OUT) :: gstif(:),ustif(:),force(:)  !global stiffness and force vector
 TYPE (ele12), POINTER :: heade   !pointer to first element

 !Local Variables
 INTEGER (kind=4), PARAMETER :: ndime =3

 TYPE (ele12), POINTER :: e               !pointer to an element
 LOGICAL  :: newmt       !if present material different than previous
 INTEGER (kind=4) :: i,j,ii,jj,g,k,l, &  !different indexes
                     isec,osec,       &  !present and old section
                     nst,             &  !
                     nvarl,ielem,nnf     !number of DOFs

 REAL (kind=8) :: d(0:109),             & !(nnn) material properties
                  deq(11),              & !Integrated Constitutive matrix to compute H vector
                  thick,                &
                  di(153)                 ! Auxiliar D matrix

 REAL (kind=8) f3(3),b3(3,nnode), & !gradient 3, C33 and associate B matrix
               f30(3),b30(3,nnode), & !gradient 3, C33 and associate B matrix
               area,                  & !mid-surface area
               ka,stm,sts,stmf,stsf     !stabilization factors

 REAL (kind=8) :: x(ndime,nnode),           &   !local coordinates
                  cq(3,2),                  &   !in-plane metric tensor at faces
                  bq(3,4,3,2),              &   !in-plane B matrix at faces
                  bm(12,4,2),               &   !in-plane B-12 matrix at mid-point on faces
                  tq(ndime,2,4,2)               !in-plane tangent vectors at sides
 REAL (kind=8) :: x0(ndime,nnode),          &   !local coordinates
                  cq0(3,2),                 &   !in-plane metric tensor at faces
                  bq0(3,4,3,2),             &   !in-plane B matrix at faces
                  bm0(12,4,2),              &   !in-plane B-12 matrix at mid-point on faces
                  tq0(ndime,2,4,2)               !in-plane tangent vectors at sides
 REAL (kind=8), ALLOCATABLE ::  bmatx(:,:),  &  !global B matrix (to be interpolated)
                                bmat0(:,:),  &  !global B matrix (to be interpolated)
                                stiff(:),stife(:,:), &!stiffness matrix
                                daux(:,:)             !constitutive matrix x dvol
 ! variables for assumed strain transverse shear
 REAL (kind=8) :: tas(3,4),      & ! der x/x_3 at Assumed Strain points
                  gas(3,4),      & ! der x/tan at Assumed Strain points
                  bs(3,8,2,2),bh(24,2,2)  ! Bs matrix at faces and stabilization part
 REAL (kind=8) :: tas0(3,4),      & ! der x/x_3 at Assumed Strain points
                  gas0(3,4),      & ! der x/tan at Assumed Strain points
                  bs0(3,8,2,2),bh0(24,2,2)  ! Bs matrix at faces and stabilization part
 ! variables for additional DOFs
 REAL (kind=8) :: hs(2,4),       & ! H vector for add DOFs
                  h(3,8),        & ! H vector for dis DOFs
                  btp(2,4,2),    & ! shear B matrix for add DOFs
                  bpp(2,4,4),    & ! in-plane B matrix for add DOFs
                  bth(2,4,2),    & ! shear hourglass b-matrix at center
                  bph(2,4,4)       ! bending hourglass b-matrix at sides
 REAL (kind=8) :: hs0(2,4),       & ! H vector for add DOFs
                  h0(3,8),h0d(32), & ! H vector for dis DOFs
                  btp0(2,4,2),    & ! shear B matrix for add DOFs
                  bpp0(2,4,4),    & ! in-plane B matrix for add DOFs
                  bth0(2,4,2),    & ! shear hourglass b-matrix at center
                  bph0(2,4,4)       ! bending hourglass b-matrix at sides


 TYPE (section), POINTER :: sec   !pointer to section

 d(0) = 0d0           !give a null value of many terms
 nvarl = ndime*nnode  !nnode*ndime number of basic element DOFs
 nst   = 11           !number of equivalent stress/strain measures
 nnf = 4              !number of nodes in a face
 IF(zigzag) THEN
   nvarl = nvarl+2*nnf    !add additional DOFs
   nst   = nst + 4 + 2    !add additional strain measures
 END IF
 ALLOCATE (stiff(nvarl*(nvarl+1)/2),bmatx(nvarl,nst))    !get memory for element stiffness matrix (upper part)
 ALLOCATE (stife(nvarl,nvarl),bmat0(nvarl,nst),daux(nst,nst))         !get memory for element stiffness matrix (upper part)

 !***  Compute Element Stiffness matrix for all elements
 bmatx = 0d0           !initializes B-matrix
 bmat0 = 0d0           !initializes B-matrix
 osec = -1             !initialize to a non-existent section
 e => heade            !point to first element

 DO
   IF (.NOT.ASSOCIATED (e) ) EXIT    !last element processed => EXIT
   isec = e%matno                    !associated section to element
   ielem = e%numel
   newmt = osec /= isec              !a different material must be considered
   IF(newmt)THEN                     !if new material, get properties
     osec = isec                     !keep present material
     sec => psecs(isec)%p            !point to section
     thick = sec%rprop(1)
     d(1:109)     = sec%rprop(6:114)
     deq(1:3) = d((/27,31,35/))/2d0-d((/14,17,19/))/thick
     deq(4:6) = d((/27,31,35/))/2d0+d((/14,17,19/))/thick
     deq(7)   = d(39)
     IF( zigzag )THEN
       di(  1: 17) = d((/ 1:4,24:26,0,0,0,0,50:53,0,0 /))    ! N1
       di( 18: 33) = d((/ 5:7,28:30,0,0,0,0,54:57,0,0 /))    ! N2
       di( 34: 48) = d((/ 8:9,32:34,0,0,0,0,58:61,0,0 /))    ! N12
       di( 49: 62) = d((/  10,36:38,0,0,0,0,62:65,0,0 /))    ! N3
       di( 63: 75) = d((/     11:13,0,0,0,0,66:69,0,0 /))    ! M1
       di( 76: 87) = d((/     15:16,0,0,0,0,70:73,0,0 /))    ! M2
       di( 88: 98) = d((/        18,0,0,0,0,74:77,0,0 /))    ! M12
       !       27,31,35,39,14,17,19,20, 0x4,78:81,0,0        ! M33
       !IF( sec%iprop(4) == 1 )THEN ! For trigonometric use computed shear factor
       !  IF( sec%rprop(4) == 0d0 )THEN !use computed shear factor
       !    d((/89,92,95,99,102,106/)) = d((/89,92,95,99,102,106/)) *sec%rprop(115)
       !    d((/90,93,96,97,100,103,104,107,108/)) = d((/90,93,96,97,100,103,104,107,108/))*sec%rprop(116)
       !    d((/91,94,98,101,105,109/)) = d((/91,94,98,101,105,109/)) *sec%rprop(117)
       !  ELSE                          !use provided shear factor
       !    d(89:108) = d(89:108)*sec%rprop(4)
       !  END IF
       !END IF
       di( 99:108) = d((/ 89,90,95,96,0,0,0,0,102,103 /))    ! S11
       di(109:117) = d((/    91,97,98,0,0,0,0,104,105 /))    ! S21
       di(118:125) = d((/       92,93,0,0,0,0,106,107 /))    ! S12
       di(126:132) = d((/          94,0,0,0,0,108,109 /))    ! S22
       di(133:138) = d((/                   40:43,0,0 /))    ! MF1
       di(139:143) = d((/                   44:46,0,0 /))    ! MF2
       di(144:147) = d((/                   47:48,0,0 /))    ! MF12
       di(148:150) = d((/                      49,0,0 /))    ! MF21
       di(151:152) = d((/                      99:100 /))    ! SF1
       di(    153) = d(                           101   )    ! SF2
       deq(8:11) = d(78:81)         !Integrated D8_mphi
       stm = di(34)*beta(1)
       sts = (di(99)+di(109)+di(118)+di(126))*beta(2)/48d0
       stmf= (di(144)+di(148))*beta(1)/2d0
       stsf= (di(151)+di(153))*beta(2)/24d0
     ELSE
       di(  1: 11) = d((/ 1:4,24:26,0,0,0,0 /))   !N11
       di( 12: 21) = d((/ 5:7,28:30,0,0,0,0 /))   !N22
       di( 22: 30) = d((/ 8:9,32:34,0,0,0,0 /))   !N12
       di( 31: 38) = d((/  10,36:38,0,0,0,0 /))   !N33
       di( 39: 45) = d((/     11:13,0,0,0,0 /))   !M11
       di( 46: 51) = d((/     15:16,0,0,0,0 /))   !M22
       di( 52: 56) = d((/        18,0,0,0,0 /))   !M12
       !     27,31,35,39,14,17,19,20,0x4        ! M33
       di( 57: 60) = d((/     89,90,95,96 /))   !!11
       di( 61: 63) = d((/        91,97,98 /))   !S21
       di( 64: 65) = d((/           92,93 /))   !S12
       di(     66) = d(                94   )   !S22
       IF( sec%rprop(4) == 0d0 )THEN !use computed shear factor
         di((/57,59,64/)) = di((/57,59,64/))*sec%rprop(115)
         di((/58,60,62,65/)) = di((/58,60,62,65/))*sec%rprop(116)
         di((/61,63,66/)) = di((/61,63,66/))*sec%rprop(117)
       ELSE                          !use provided shear factor
         di(57:66) = di(57:66)*sec%rprop(4)
       END IF
       sts = (di(57)+di(61))*beta(2)/24d0
       stm = di(22)*beta(1)
     END IF
     newmt = .FALSE.               !same material than previous ?
     j = 1
   END IF !newmt
   ! new configuration
   area = e%dvol/thick    !area
   x(:,1:nnode) = coora(:,e%lnods(1:nnode))     !nodes of the element
   x0(:,1:nnode) = coord(:,e%lnods(1:nnode))     !nodes of the element
   ! compute in-plane gradient at each mid-side point of upper and lower faces
   DO ii=1,2 ! for each face
     i = 1+nnf*(ii-1)     !first node of the face
     DO jj=1,nnf  !for each mid side point
       tq(:,1:2,jj,ii) = MATMUL(x(:,i:i+3),e%cdq(1:nnf,1:2,jj,ii))  !main element contribution
       tq0(:,1:2,jj,ii) = MATMUL(x0(:,i:i+3),e%cdq(1:nnf,1:2,jj,ii))  !main element contribution
     END DO
     !compute transvere shear strain components
     tas(:,:) = MATMUL(x,e%nfdas(:,:,ii))  !der x/x_3 at the 4 mid-side points
     tas0(:,:) = MATMUL(x0,e%nfdas(:,:,ii))  !der x/x_3 at the 4 mid-side points
     IF( ii == 1 )THEN      !bottom face
       gas(:,1) = (x(:,4) -x(:,1))/2d0 !point A der x/eta
       gas(:,2) = (x(:,2) -x(:,1))/2d0 !point B der x/xita
       gas(:,3) = (x(:,3) -x(:,2))/2d0 !point C der x/eta
       gas(:,4) = (x(:,3) -x(:,4))/2d0 !point D der x/xita
       CALL bsma12s(gas,tas,e%nfdas(:,:,1),bs(:,:,:,1),e%jacin(:,:,1),1,bh(:,:,1))     !compute B_s matrix at sampling points
       gas0(:,1) = (x0(:,4) -x0(:,1))/2d0 !point A der x/eta
       gas0(:,2) = (x0(:,2) -x0(:,1))/2d0 !point B der x/xita
       gas0(:,3) = (x0(:,3) -x0(:,2))/2d0 !point C der x/eta
       gas0(:,4) = (x0(:,3) -x0(:,4))/2d0 !point D der x/xita
       CALL bsma12s(gas0,tas0,e%nfdas(:,:,1),bs0(:,:,:,1),e%jacin(:,:,1),1,bh0(:,:,1))     !compute B_s matrix at sampling points
     ELSE
       gas(:,1) = (x(:,8) -x(:,5))/2d0 !point A der x/eta
       gas(:,2) = (x(:,6) -x(:,5))/2d0 !point B der x/xita
       gas(:,3) = (x(:,7) -x(:,6))/2d0 !point C der x/eta
       gas(:,4) = (x(:,7) -x(:,8))/2d0 !point D der x/xita
       CALL bsma12s(gas,tas,e%nfdas(:,:,2),bs(:,:,:,2),e%jacin(:,:,2),2,bh(:,:,2))     !compute B_s matrix at sampling points
       gas0(:,1) = (x0(:,8) -x0(:,5))/2d0 !point A der x/eta
       gas0(:,2) = (x0(:,6) -x0(:,5))/2d0 !point B der x/xita
       gas0(:,3) = (x0(:,7) -x0(:,6))/2d0 !point C der x/eta
       gas0(:,4) = (x0(:,7) -x0(:,8))/2d0 !point D der x/xita
       CALL bsma12s(gas0,tas0,e%nfdas(:,:,2),bs0(:,:,:,2),e%jacin(:,:,2),2,bh0(:,:,2))     !compute B_s matrix at sampling points
     END IF
   END DO

   CALL bmat12s(e%cdq,bq,tq,bm,e%area) !in-plane b-matrix at faces and stabilization
   CALL bmat12s(e%cdq,bq0,tq0,bm0,e%area) !in-plane b-matrix at faces and stabilization

   ! EAS
   f3 = MATMUL(x,e%cartd)             ! x_3 at element center
   DO i=1,nnode
     b3(:,i) = f3*e%cartd(i)          ! B33 at element center
   END DO
   f30 = MATMUL(x0,e%cartd)             ! x_3 at element center
   DO i=1,nnode
     b30(:,i) = f30*e%cartd(i)          ! B33 at element center
   END DO

   DO i=1,nnf !for each face node
     j  = i+nnf  !for each other face node
     h0(:,i) = deq( 1)*bq0(:,i,1,1) + deq( 2)*bq0(:,i,2,1) + deq( 3)*bq0(:,i,3,1) + &
               deq( 7)*b30(:,i)
     h0(:,j) = deq( 4)*bq0(:,i,1,2) + deq( 5)*bq0(:,i,2,2) + deq( 6)*bq0(:,i,3,2) + &
               deq( 7)*b30(:,j)
     h (:,i) = deq( 1)*bq (:,i,1,1) + deq( 2)*bq (:,i,2,1) + deq( 3)*bq (:,i,3,1) + &
               deq( 7)*b3 (:,i)
     h (:,j) = deq( 4)*bq (:,i,1,2) + deq( 5)*bq (:,i,2,2) + deq( 6)*bq (:,i,3,2) + &
               deq( 7)*b3 (:,j)
   END DO
   e%h(1:24)  = RESHAPE( h, (/24/))*area    !keep EAS vector
   h0d(1:24)  = RESHAPE( h0, (/24/))*area   !put into a vector

   IF( zigzag )THEN   ! compute BS_Phi matrix at element center
     CALL bphi12s(e%area(:,1),e%cdq(:,:,:,1),e%asazz,btp,bpp,e%nangl,e%jacin(:,:,1),bth,bph)
     CALL bphi12s(e%area(:,1),e%cdq(:,:,:,1),e%asazz,btp0,bpp0,e%nangl,e%jacin(:,:,1),bth0,bph0)
     DO i=1,nnf
       hs0(:,i) = MATMUL(bpp0(:,i,:),deq(8:11))
       hs (:,i) = MATMUL(bpp (:,i,:),deq(8:11))
     END DO
     e%h(25:32)  = RESHAPE( hs , (/8/))*area    !keep EAS vector
     h0d(25:32)  = RESHAPE( hs0, (/8/))*area    !put into a vector
   END IF

   k=0  !scale constitutive matrix
   DO i=1,nst
     DO j=i,nst
       k= k+1
       daux(i,j) = di(k)*area
       daux(j,i) = daux(i,j)
     END DO
   END DO
   ! Build complete B matrix (shell type Bm B3 Bb Bs Bf )
   CALL bmat12(bmatx,bq,b3,bs,thick,.FALSE.,zigzag,bpp,btp,nnf)
   CALL bmat12(bmat0,bq0,b30,bs0,thick,.FALSE.,zigzag,bpp0,btp0,nnf)
   stife = 0d0                     !initializes stiffness matrix
   CALL b0dbpr(bmat0(1,1),bmatx(1,1),daux(1,1),stife(1,1),nvarl,nst) ! B0^T D B
   !WRITE(58,"('standard stiffness')")
   DO i=1,nvarl
     !DO j=1,nvarl
     !   IF( ABS(stife(i,j) - stife(j,i)) > 1d0 )WRITE(58,"(2i5,2e15.5)")i,j,stife(i,j),stife(j,i)
     !END DO
   !  write(58,"(32e15.6)")(stife(i,j),j=1,nvarl)
   END DO
   !Membrane Stabilization
   ka = area*stm
   CALL stbm12a(stife,bm0,bm,ka,e%area,nvarl)
   !WRITE(58,"('after membrane stabilization')")
   DO i=1,nvarl
     !DO j=1,nvarl
     !   IF( ABS(stife(i,j) - stife(j,i)) > 1d0 )WRITE(58,"(2i5,2e15.5)")i,j,stife(i,j),stife(j,i)
     !END DO
   !  write(58,"(32e15.6)")(stife(i,j),j=1,nvarl)
   END DO
   !Shear Stabilization
   ka = area*sts
   CALL stbs12a(stife,bh0,bh,ka,e%jacin,nvarl)
   !WRITE(58,"('after shear stabilization')")
   DO i=1,nvarl
     !DO j=1,nvarl
     !   IF( ABS(stife(i,j) - stife(j,i)) > 1d0 )WRITE(58,"(2i5,2e15.5)")i,j,stife(i,j),stife(j,i)
     !END DO
   !  write(58,"(32e15.6)")(stife(i,j),j=1,nvarl)
   END DO
   ! membrane and shear additional DOFs stabilization
   IF( zigzag )THEN
     ka = area*stmf
     CALL stbm12fa(stife,RESHAPE( bph0,(/8,4/)),RESHAPE( bph,(/8,4/)),ka,e%area(:,1))
   !  WRITE(58,"('after ZZ memb stabilization')")
     DO i=1,nvarl
       !DO j=1,nvarl
       !   IF( ABS(stife(i,j) - stife(j,i)) > 1d0 )WRITE(58,"(2i5,2e15.5)")i,j,stife(i,j),stife(j,i)
       !END DO
   !    write(58,"(32e15.6)")(stife(i,j),j=1,nvarl)
     END DO
     ka = area*stsf
     CALL stbs12fa(stife,RESHAPE( bth0,(/8,2/)),RESHAPE( bth,(/8,2/)),ka,e%jacin(:,:,1))
   !  WRITE(58,"('after ZZ shear stabilization')")
     DO i=1,nvarl
       !DO j=1,nvarl
       !   IF( ABS(stife(i,j) - stife(j,i)) > 1d0 )WRITE(58,"(2i5,2e15.5)")i,j,stife(i,j),stife(j,i)
       !END DO
   !    write(58,"(32e15.6)")(stife(i,j),j=1,nvarl)
     END DO
   END IF
   ! condense EAS parameter
   !write(58,"(e15.5)")e%ka
   !!write(58,"(32e15.6)")e%h
   !write(58,"(32e15.6)")h0d
   CALL cond12i(stife,h0d,e%h,e%ka,nvarl)
   !WRITE(58,"('after alpha condensation ')")
   DO i=1,nvarl
   !  write(58,"(32e15.6)")(stife(i,j),j=1,nvarl)
   END DO
   ! add to global stiffness
   IF( nsymm == 1 )THEN
     ! a new routine must be written
     CALL stiaux_ss_NS(nnode,e%lnods(1),nvarl,zigzag,stife(1,1),force(1),gstif(1),ustif(1) )
   ELSE
     k = 0
     DO i=1,nvarl
       k = k+1
       stiff(k) = stife(i,i)
       DO j=i+1,nvarl
         k = k+1
         stiff(k) = (stife(i,j)+stife(j,i))/2d0
       END DO
     END DO
     CALL stiaux_ss(nnode,e%lnods,nvarl,zigzag,stiff(1),force(1),gstif(1) )
   END IF
   e => e%next
 END DO
RETURN

 END SUBROUTINE stif12si
!!-----------------------------------------------------------
 SUBROUTINE cond12i(k,h0,h,ka,n)
 IMPLICIT NONE
 ! dummy arguments
 INTEGER(kind=4), INTENT(IN) :: n
 REAL(kind=8), INTENT(IN) :: ka,h0(:),h(:)
 REAL(kind=8), INTENT(IN OUT) :: k(:,:)
 ! local variables
 INTEGER(kind=4) :: i,j
 REAL(kind=8) :: f

 DO i=1,n
   f = h0(i)/ka
   DO j=1,n
     k(i,j) = k(i,j) - f*h(j)
   END DO
 END DO

 RETURN

 END SUBROUTINE cond12i
!-----------------------------------------------------------
 SUBROUTINE stbm12a(s,b0,b,ka,area,nvarl)
 IMPLICIT NONE
 ! dummy arguments
 INTEGER(kind=4), INTENT(IN) :: nvarl
 REAL(kind=8), INTENT(IN) :: ka,b0(:,:,:),b(:,:,:),area(:,:)
 REAL(kind=8), INTENT(IN OUT) :: s(:,:)
 ! local variables
 INTEGER(kind=4) :: i,j,k,l,n,m,l0,la
 REAL(kind=8) :: f

 l = 0   !first position in stiff for first face
 n = 12  !last position in stiff for first face
 DO m=1,2  !for each face
   DO k=1,4        !for each mid-side point
     DO i=1,n      !for each DOF in the face
       f = b0(i,k,m)*ka*area(k,m)
       DO j=1,n
         s(i+l,j+l) = s(i+l,j+l) + f*b(j,k,m)
       END DO
     END DO
   END DO
   l = l+12       !first position in stiff for second face
 END DO

 RETURN

 END SUBROUTINE stbm12a
!!-----------------------------------------------------------
 SUBROUTINE stbs12a(s,bh0,bh,ka,ji,nvarl)
 ! shear stabilization matrix
 IMPLICIT NONE
 ! dummy arguments
 REAL(kind=8), INTENT(IN) :: ka,bh0(:,:,:),bh(:,:,:),ji(:,:,:)
 REAL(kind=8), INTENT(IN OUT) :: s(:,:)
 INTEGER(kind=4), INTENT(IN) :: nvarl
 ! local variables
 INTEGER(kind=4) :: i,j,m
 REAL(kind=8) :: a,b,f1,f2


 DO m=1,2  !for each face
   a = (ji(1,1,m)*ji(1,1,m) + ji(2,1,m)*ji(2,1,m))*ka
   b = (ji(1,2,m)*ji(1,2,m) + ji(2,2,m)*ji(2,2,m))*ka
   DO i=1,24       !for each DOF
     f1 = bh0(i,1,m)*a
     f2 = bh0(i,2,m)*b
     DO j=1,24     !for each DOF
       s(i,j) = s(i,j) + f1*bh(j,1,m) + f2*bh(j,2,m)
     END DO
   END DO
 END DO

 RETURN

 END SUBROUTINE stbs12a
!!-----------------------------------------------------------
  SUBROUTINE stbm12fa(s,b0,b,ka,area)
  IMPLICIT NONE
  ! dummy arguments
  REAL(kind=8), INTENT(IN) :: ka,b0(:,:),b(:,:),area(:)
  REAL(kind=8), INTENT(IN OUT) :: s(:,:)
  ! local variables
  INTEGER(kind=4) :: i,j,k,l,m,l0
  REAL(kind=8) :: f1,f

    l0 = 24         !initializes at last position in STIFF
    DO k=1,4        !for each mid-side point
      f1 = ka*area(k)
      DO i=1,8      !for each additional DOF
        f = b0(i,k)*f1
        DO j=1,8
          s(i+l0,j+l0) = s(i+l0,j+l0) + f*b(j,k)
        END DO
      END DO
    END DO

  RETURN

  END SUBROUTINE stbm12fa
!!-----------------------------------------------------------
  SUBROUTINE stbs12fa(s,bh0,bh,ka,ji)
  !
  IMPLICIT NONE
  ! dummy arguments
  REAL(kind=8), INTENT(IN) :: ka,bh0(:,:),bh(:,:),ji(:,:)
  REAL(kind=8), INTENT(IN OUT) :: s(:,:)
  ! local variables
  INTEGER(kind=4) :: i,j,l0,m
  REAL(kind=8) :: a,b,f1,f2

    a = (ji(1,1)*ji(1,1) + ji(2,1)*ji(2,1))*ka
    b = (ji(1,2)*ji(1,2) + ji(2,2)*ji(2,2))*ka
    l0 = 24         !initializes at last position in STIFF
    DO i=1,8        !for each DOF
      f1 = bh0(i,1)*a
      f2 = bh0(i,2)*b
      DO j=1,8
        s(i+l0,j+l0) = s(i+l0,j+l0) + f1*bh(j,1) + f2*bh(j,2)
      END DO
    END DO

  RETURN

  END SUBROUTINE stbs12fa
!!-----------------------------------------------------------
