 SUBROUTINE elmd12 (nelem, heade, taile, iwrit, nnode, nstra, nnb)
 !******************************************************************
 !
 !*** READ element DATA for 3-D SOLID element  (TLF)
 !
 !******************************************************************
 IMPLICIT NONE
 ! dummy arguments
 INTEGER (kind=4) :: nelem, iwrit, nnode, nstra, nnb
 TYPE (ele12), POINTER :: heade,taile !pointers to first and last elements
 ! local variables
 TYPE(section), POINTER :: sec        !pointer to a section
 LOGICAL found
 INTEGER (kind=4) :: i,ip,j,chnode,isec,osec,secty,mtype,nna

 INTEGER (kind=4) :: mats(2) = (/ 1,5 /)  !acceptable material types
 TYPE (ele12), POINTER :: elem      !auxiliar pointer

 ! *** READ the element nodal connections, and the property numbers.

 IF(iwrit == 1) WRITE(lures,"(//2X,'ELEMENT MAT           NODE NUMBERS ',&
                              &    4x,'angle')",ERR=9999)

 osec = -1  !initializes
 j = 2+nnb  !last integer value to read
 nna = nnb/2  !number of mid-side points & number of nodes in each face
 DO
   ! loop over elements

   CALL listen('ELMD12')          !read a card
   IF (exists('ENDELE')) EXIT     !if key word END_ELEMENT read, exit loop

   nelem = nelem + 1         !increase element counter
   CALL new_ele12e (elem)    !reserve memory for data
   ALLOCATE( elem%lnods(nnode))  !reserve memory for connectivities

   elem%numel = INT (param(1))   !element label

   isec = INT (param(2))         !associated material

   IF( isec /= osec )THEN        !checks
     CALL sect_search (isec,found,sec,ip)
     IF(.NOT.found)CALL runend('ELMD12: Invalid Section Number used')
     secty = sec%secty        !check section type for element
     IF( secty /= 5 ) CALL runend('ELMD12: Invalid sect. type for SOLID')
     IF( sec%mabas >= 0 )THEN
       mtype = sec%mtbas%mtype  !check material type for element
       IF( .NOT. ANY(mtype == mats) )  &
         CALL runend('ELMD12: Invalid Mat. type for SOLSH')
     END IF
     osec = isec
   END IF

   elem%matno = ip        !store section position in array

   elem%lnods = INT (param(3:j))  !connectivities
   elem%angle = param(j+1)        ! Euler angle between standart and Ortho
   !    echo according to flag
   IF(iwrit == 1)THEN
      IF(nnb == 6 )THEN
        WRITE(lures,"(i6,i3,6i6,F7.4)",ERR=9999) elem%numel,isec,elem%lnods(1:nnb),elem%angle
      ELSE
        WRITE(lures,"(i6,i3,8i6,F8.3)",ERR=9999) elem%numel,isec,elem%lnods(1:nnb),elem%angle
      END IF
   END IF
   !     original node labels are changed
   DO i=1,nnb
     elem%lnods(i) = chnode(elem%lnods(i))
   END DO
   ! reserve memory for arrays and initializes
   ALLOCATE(elem%cartd(nnb),       &  ! cartesian der (y3) of Shape Functions at center (EAS)
            elem%nfdas(nnb,nna,2), &  ! Nodal Function Derivatives at the Assumed Strain points
            elem%stint(nstra))        ! integrated stresses

   IF( nnb == 6 ) THEN !SPRISM
     ALLOCATE(elem%se(17))          !11
     IF( nnode == 12 ) THEN        !quad
       ALLOCATE (elem%cdq(4,2,3,2))
       elem%lnods(nnb+1:nnode) = 0     !initializes extra nodes (if exist)
     ELSE !IF( nnb == 6 )THEN
       ALLOCATE (elem%cdq(nna,2,1,2))
     END IF
   ELSE  ! SSOLAG
     ALLOCATE(elem%area(nna,2), elem%cdq(nna,2,nna,2),elem%se(25))  !19
   END IF

   IF( nstra == 8 ) THEN
     ALLOCATE (elem%h(3*nnode) )
   ELSE  !additional DOFs
     ALLOCATE (elem%h(3*nnode+2*nna) )
     ALLOCATE( elem%nangl(2,nna))
     elem%nangl(1,:) = 1d0
     elem%nangl(2,:) = 0d0
     IF( nnb == 8 )ALLOCATE(elem%asazz(2,nna))
   END IF
   elem%h     = 0d0
   elem%se    = 0d0      !     "     Equivalent stress for geometric stiffness
   elem%stint = 0d0

   CALL add_ele12e (elem, heade, taile) !add element to list

 END DO

 RETURN
 9999 CALL runen2('')
 END SUBROUTINE elmd12
