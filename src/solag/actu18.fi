 SUBROUTINE actu18(heade,ngaus)
 IMPLICIT NONE
 TYPE(ele18), POINTER :: heade
 INTEGER (kind=4) :: ngaus

 INTEGER (kind=4) :: n,m,isec,j,nucom,osec,g
 INTEGER (kind=4), PARAMETER :: nvar=7,  nstre=6
 REAL(kind=8) :: defps
 LOGICAL :: newmt
 TYPE(ele18), POINTER :: e
 TYPE (section), POINTER :: sec    !pointer to a section data

 osec = -1
 e => heade
 DO
   IF( .NOT.ASSOCIATED(e) ) EXIT
   IF( SIZE(e%gausv,1) > 1 ) THEN   !Yield must be > zero
     isec = e%matno                   !associated material
     newmt = osec /= isec             !same material than previous ?
     IF(newmt)THEN                     !if new material, get properties
       sec => psecs(isec)%p
       osec = isec                    !keep present material
       nucom = sec%iprop(2)           !number of material components
     END IF
     DO g=1,ngaus
       m = 1   !pointer to last converged
       DO j=1,nucom
         n = m+nvar    !position of current values
         defps = e%gausv(n+nstre,g)  !pointer to increment in plastic strain
         IF(defps > 0)THEN
           e%gausv(m:m+nstre-1,g) = e%gausv(n:n+nstre-1,g) !current into last converged
           e%gausv(m+nstre,g) = e%gausv(m+nstre,g) + defps  !eff. pl. strain
         END IF
         m = m+nvare    !position of last converged of next component in layer
       END DO
     END DO
   END IF
   e => e%next
 END DO

 RETURN
 END SUBROUTINE actu18
