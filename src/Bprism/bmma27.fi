 SUBROUTINE bmma27(tq,bm,ansmm,ipcdm)
 !
 !*** B membrame (ANS) matrix  for a 3-D 12-node Prism solid-shell element

 IMPLICIT NONE
 ! dummy arguments
 REAL (kind=8), INTENT(IN) ::  tq(:,:,:,:)     !(ndime,2,nface,nvert) in-plane gradient at each SP at faces
 REAL (kind=8), INTENT(OUT) :: bm(:,:,:,:,:)   !(ndime,nnode,3,nvert,nface) in-plane matrix at each SP at faces
 INTEGER (kind=4), INTENT(IN) :: ansmm         !Assumed Natural Strain Membrane Model
 REAL (kind=8), POINTER, INTENT(IN) :: ipcdm(:,:,:,:)   !(nnod1,2,nface,ngaup) in-plane Cartesian derivatives

 ! local variables
 INTEGER (kind=4) :: face,vert,gp,inode,jj     !indexes

 ! in-plane components
 DO face=1,nface   !for each face
   IF (ansmm == 0) THEN
     DO gp=1,ngaup  !for each sampling point (Gauss Point)
       DO inode = 1,nnode
         bm(1:3,inode,1,gp,face) = tq(1:3,1,face,gp)*ipcdm(inode,1,face,gp) !x-x
         bm(1:3,inode,2,gp,face) = tq(1:3,2,face,gp)*ipcdm(inode,2,face,gp) !y-y
         bm(1:3,inode,3,gp,face) = tq(1:3,1,face,gp)*ipcdm(inode,2,face,gp)+tq(1:3,2,face,gp)*ipcdm(inode,1,face,gp) !x-y
       END DO
     END DO
   ELSE
     DO jj=1,3      !for each sampling point (mid side point of each subtriangle)
       DO inode = 1,nnode
         bm(1:3,inode,jj,1,face) = tq(1:3,jj,face,1)*mdtan(inode,jj  ,face) !xi-xi
         bm(1:3,inode,jj,2,face) = tq(1:3,jj,face,2)*mdtan(inode,jj+3,face) !eta-eta
         bm(1:3,inode,jj,3,face) = tq(1:3,jj,face,3)*mdtan(inode,jj+6,face) !t-t
       END DO
     END DO
   END IF
 END DO

 RETURN
 END SUBROUTINE bmma27
