 SUBROUTINE kgms27 (nfdas,ses,stiff,nassp)
 !
 ! compute geometric stiffness for BPRISM - transverse shear
 !

 IMPLICIT NONE
 ! dummy arguments
 INTEGER (kind=4), INTENT(IN) :: nassp
 REAL (kind=8), INTENT(IN) ::  nfdas(nnode,nassp,2), & !cartesian (y3)  derivatives at points
                               ses(nassp,2)            !integrated stresses at each face
 REAL (kind=8), INTENT(OUT) :: stiff(*)        !stiffness matrix
 ! local variables
 INTEGER (kind=4) i,j,k,l,pos,nv2,f,n1,n2
 REAL (kind=8) :: value                 !diagonal value to add
 INTEGER (kind=4) poesti
 poesti(nv2,i,j) = (nv2-i)*(i-1)/2+j    ! position i,j in stiff(nxn)

 nv2 = 6*nnode    !72

 DO f=1,2   ! for each face

   n1 = 0     !previous DOF for i-node
   DO i=1,nnode      ! for each element node
     n2 = (f-1)*18     !previous DOF for j-node
     DO j=1,nnode      ! for each face node
       value = 0d0     ! initializes
       DO k=1,nassp ! for each sampling point
         value = value + ses(k,f)*nfdas(i,k,f)*sdtan(j,k,f)
       END DO
       IF(n1 <= n2)THEN   !above diagonal
         IF(n1 == n2)value = 2d0*value
         DO l=1,3      ! for each DOF (diagonal only)
           pos = poesti(nv2,n1+l,n2+l)   ! (I,J)
           stiff(pos) = stiff(pos) + value
         END DO
       ELSE               !below diagonal
         DO l=1,3      ! for each DOF (diagonal only)
           pos = poesti(nv2,n2+l,n1+l)   ! (I,J)
           stiff(pos) = stiff(pos) + value
         END DO
       END IF
       n2= n2+ 3    !update previous DOF for j-node
     END DO
     n1 = n1+3  !update previous DOF for i-node
   END DO

 END DO

 RETURN
 END SUBROUTINE kgms27
