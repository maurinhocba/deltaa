 SUBROUTINE bmai27(bm,bs,jam,jas,bm1,bs1,gp,ansmm,anssh,nassp)
 !*** Compute
 !***         Cartesian Bbar matrices at external faces
 !***         to be intepolated across the thickness
 !***    for a 3-D 12-node Prism solid-shell element
 !***    Green Lagrange Strains (TLF)
 IMPLICIT NONE
 ! dummy arguments
 REAL (kind=8), INTENT(IN) :: bm(:,:,:,:,:),  & !(ndime,nnode,3,ngaup,nface) in-plane matrix at faces (natural strains)
                              bs(:,:,:,:),    & !(ndime,nnode,nassp,nface) assumed transverse shear matrix at faces (mixed strains)
                              jam(:,:,:),     & !(2,2,nface) inverse jacobian at in-plane GP at each face
                              jas(:,:,:)        !(2,2,nface) inverse jacobian at in-plane GP at each face

 REAL (kind=8), INTENT(OUT) :: bm1(:,:,:,:)     !(ndime,nnode,3,2) Bmembrane -matrix to interpolate
 REAL (kind=8), INTENT(OUT) :: bs1(:,:,:,:)     !(ndime,nnode,2,2) Bshear    -matrix to interpolate

 INTEGER (kind=4), INTENT(IN) :: gp,    &       !in-plane GP
                                 ansmm, &       !Assumed Natural Strain Membrane Model
                                 anssh, &       !Assumed Natural Strain SHear model
                                 nassp          !number of assumed shear sampling points

 ! local variables
 INTEGER (kind=4) :: face,node,j,k              !indexes
 REAL (kind=8) :: ba(3,3),bb(3,2),f(3,3),ji(2,2)

 ! in-plane components
 DO face=1,nface
   ! Do not transpose Jacin
   ! membrane matrix (cartesian strains) at integration points (faces)
   SELECT CASE (ansmm)   !according to Membrane Model
   CASE (0)          !standard displacement formulation
     bm1(:,:,:,face) = bm(:,:,:,gp,face)
   CASE (1:2)               ! ANS
     ji = jam(:,:,face)     !inverse in-plane jacobian at integration point of the face
     !  Transpose bar T
     f = RESHAPE( (/ ji(1,1)**2,          ji(2,1)**2,          ji(1,1)*ji(2,1), &
                     ji(1,2)**2,          ji(2,2)**2,          ji(1,2)*ji(2,2), &
                     2d0*ji(1,1)*ji(1,2), 2d0*ji(2,1)*ji(2,2), ji(1,1)*ji(2,2)+ji(1,2)*ji(2,1)/),(/3,3/))
     !WRITE(58,"(3e15.5)")f                 !3x3
     !WRITE(58,"(3e15.5)")mpag2(:,:,gp)      !3x9
     !WRITE(58,"(6e15.5)")bm(1:2,:,:,:,1)   !2x6x9
     DO node=1,nnode
       ba = MATMUL(RESHAPE((/ bm(:,node,:,:,face) /),(/3,nasmm/)),TRANSPOSE(pagm2(:,:,gp)))
       bm1(1:3,node,1:3,face) = MATMUL(ba,f)   !ok
     END DO
   END SELECT
   ! shear matrix
   SELECT CASE (anssh)   !according to Membrane Model
   CASE ( 0 )
     j = 2*gp-1
     bs1(:,:,1:2,face) = bs(:,:,j:j+1,face)
   CASE ( 1:2 )
     ji = jas(:,:,face)     !inverse in-plane jacobian at integration point of the face
     DO node=1,nnode
       bb(1:3,1:2) = MATMUL(bs(1:3,node,:,face),TRANSPOSE(pags(1:2,1:nassp,gp)))  !pag(2,nassp,ngaup)
       bs1(1:3,node,1:2,face) = MATMUL(bb,ji)  !ok
     END DO
   END SELECT
 END DO

 RETURN
 END SUBROUTINE bmai27
