 SUBROUTINE rest27 (nelem, nreqs, heade, taile, ngrqs, ngaus)
 !******************************************************************
 !
 !***  restore an element set at restart for solid-shell element BPrism
 !
 !******************************************************************
 IMPLICIT NONE
 ! dummy arguments
 INTEGER (kind=4), INTENT(IN) :: nreqs, & !Number of elements for requested output
                                 ngaus, & !number of integration points
                                 nelem    !number of elements in the set
 INTEGER (kind=4), POINTER  :: ngrqs(:) !elements for requested output
 TYPE (ele27), POINTER  :: heade,taile  !pointers to first and last element
 ! local variables
 INTEGER (kind=4) :: i,j,ielem,size1,size2
 TYPE (ele27), POINTER :: e

 DO ielem=1,nelem    !for each element

   CALL new_ele27e (e)    !reserve memory for data
   ALLOCATE (e%stint(nstre,ngaus,ngaup))  !get memory
   READ (51) e%numel,e%matno,e%lnods,size1,size2
   READ (51) e%angle,e%dvol,e%cartd,e%nfdas,e%jacin,e%alpha,e%ka,e%se
   READ (51) e%h,e%stint
   IF( size1 > 0 )THEN
     ALLOCATE (e%gausv(size1,size2))             !get space for internal vars
     READ (51) ((e%gausv(i,j),i=1,size1),j=1,size2) !read internal variables
   END IF

   CALL add_ele27e (e, heade, taile)           !add element to the list

 END DO
 IF ( nreqs > 0 )THEN
   ALLOCATE(ngrqs(nreqs))
   READ (51) (ngrqs(i), i=1,nreqs)  !read elements for output
 END IF

 RETURN
 END SUBROUTINE rest27
