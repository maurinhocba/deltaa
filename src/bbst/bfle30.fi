 SUBROUTINE bfle30(lb,a,b,bb,t,t3,h,cdn)
 !
 !     Computes bending B matrix for element type 30
 !
 IMPLICIT NONE

 REAL (kind=8), INTENT(IN) :: lb,                &!A0/A == t/t0
                              a(3),b(3),         &!Element side proyections
                              cdn(6,3),          &!side normal derivatives ut.
                              t(3,2),            &!configuration derivatives
                              t3(3),             &!normal vector
                              h(3,3)              !h vectors

 REAL (kind=8), INTENT(OUT) ::  bb(3,6,3) !Bending B matrix

 INTEGER (kind=4) i,j
 REAL (kind=8) d1(3),d2(3),n12,n22,n1n2,r(3,2) !!
 REAL (kind=8) ::  bl(6,3) !local Bending B matrix

 !***  bending matrix evaluation

 bl = 0d0                      !zero B matrix

 ! Contravariant base vectors of the main element
 d1(1) = (t(2,2)*t3(3) - t(3,2)*t3(2))*lb
 d1(2) = (t(3,2)*t3(1) - t(1,2)*t3(3))*lb
 d1(3) = (t(1,2)*t3(2) - t(2,2)*t3(1))*lb
 d2(1) = (t3(2)*t(3,1) - t3(3)*t(2,1))*lb
 d2(2) = (t3(3)*t(1,1) - t3(1)*t(3,1))*lb
 d2(3) = (t3(1)*t(2,1) - t3(2)*t(1,1))*lb
 ! ro components
 r(1,1) = DOT_PRODUCT(h(:,1) ,d1)
 r(2,1) = DOT_PRODUCT(h(:,2) ,d1)
 r(3,1) = DOT_PRODUCT(h(:,3) ,d1)
 r(1,2) = DOT_PRODUCT(h(:,1) ,d2)
 r(2,2) = DOT_PRODUCT(h(:,2) ,d2)
 r(3,2) = DOT_PRODUCT(h(:,3) ,d2)

 DO i=1,3      ! for each mid-side point, and for each main triangle node
   !  Contributions from normal displacements of the Main element
   bl(i,1) = bl(i,1) + r(1,1)*b(i) - r(1,2)*a(i)          !K11
   bl(i,2) = bl(i,2) + r(2,1)*b(i) - r(2,2)*a(i)          !K22
   bl(i,3) = bl(i,3) + (r(3,1)*b(i) - r(3,2)*a(i))*2d0    !2K12
   !  Contributions from normal displacements at mid-side points
   n12 = -b(i)*b(i)
   n22 = -a(i)*a(i)
   n1n2= +a(i)*b(i)*2d0
   DO j=1,6                        ! for each node
     bl(j,1)= bl(j,1) +  n12  * cdn(j,i)              !K11
     bl(j,2)= bl(j,2) +  n22  * cdn(j,i)              !K22
     bl(j,3)= bl(j,3) +  n1n2 * cdn(j,i)              !2K12
   END DO
 END DO

 DO j=1,3        !for each curvature
   DO i=1,6      !for each node in the element
     bb(:,i,j) = bl(i,j)*t3
   END DO
 END DO
 RETURN
 END SUBROUTINE bfle30
