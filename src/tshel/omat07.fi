 SUBROUTINE omat07(ansmm,posgp,omat)
 ! computes O matrix for Assumed Natural Strain Membrane Models
 ! for quadratic shell triangle
 IMPLICIT NONE
 INTEGER (kind=4), INTENT(IN) :: ansmm
 REAL(kind=8), INTENT(IN) :: posgp(2,ngaus)
 REAL(kind=8), INTENT(OUT) :: omat(3,nasmm,2,ngaus)

 INTEGER(kind=4) :: g
 REAL(kind=8) :: pmat(3,nasmm)

 IF( ansmm == 1 )THEN
   omat = 0d0
 ELSE
   pmat = 0d0 ; pmat(1,1) = 1d0 ; pmat(2,4) = 1d0 ; pmat(3,7) = 1d0
 END IF
 DO g=1,ngaus
   IF( ansmm == 1 )THEN
     omat(1,1,1,g) = 1d0-posgp(1,g)-posgp(2,g); omat(2,2,1,g) = omat(1,1,1,g); omat(3,3,1,g) = omat(1,1,1,g)
     omat(1,4,1,g) = posgp(1,g);                omat(2,5,1,g) = omat(1,4,1,g); omat(3,6,1,g) = omat(1,4,1,g)
     omat(1,7,1,g) = posgp(2,g);                omat(2,8,1,g) = omat(1,7,1,g); omat(3,9,1,g) = omat(1,7,1,g)
     omat(:,:,2,g) = omat(:,:,1,g)
   ELSE
     pmat(1,2) = posgp(1,g); pmat(2,5) = posgp(1,g); pmat(3,8) = posgp(1,g)
     pmat(1,3) = posgp(2,g); pmat(2,6) = posgp(2,g); pmat(3,9) = posgp(2,g)
     omat(:,:,1,g) = MATMUL(pmat,amat2)
     omat(1:2,:,2,g) = omat(1:2,:,1,g)
     omat(3,:,2,g) = omat(3,:,1,g)*2d0
   END IF
 END DO
 RETURN

 END SUBROUTINE omat07
