 SUBROUTINE istgp7(nst,s,e,d,ambda,matty,b,sf)
 !*****************************************************************************
 !
 !*****evaluates total resultant stresses for shell element
 !     for linear elastic material
 !
 !****************************************************************************
 IMPLICIT NONE
 ! ngaus : number of in-plane Gauss Points
 !                        routine parameters
 INTEGER (kind=4), INTENT(IN) :: nst, & ! number of strain-measures  = 8
                                 matty  ! material model

 REAL (kind=8), INTENT(IN) :: e(nst,ngaus),    & ! strain measures
                              ambda(2,ngaus),  & ! transverse ratios
                              d(15),           & ! isotropir-orthotropic D-matrix
                              b(21),           & ! anisotropic coupling
                              sf(3)              ! anisotropic shear factors
 REAL (kind=8), INTENT(OUT) :: s(nst,ngaus)  !integrated stresses

 !                        local variables
 INTEGER (kind=4) :: g  !Gauss point Index
 REAL    (kind=8) :: h  !present thickness ratio

 DO g = 1,ngaus
   h = ambda(2,g)   !h = 1d0
   !                     compute effective stresses
   SELECT CASE (matty)   !according to material model
   CASE (1:2)         !isotropic  small or large strains            E11   E22   E12   C11   C22   C12   G1    G2
     s(1,g) = d(1) * e(1,g) + d(2) * e(2,g)                    !N11   1     2
     s(2,g) = d(2) * e(1,g) + d(1) * e(2,g)                    !N22   2     1
     s(3,g) = d(3) * e(3,g)                                    !N12               3
     s(4,g) =(d(4) * e(4,g) + d(5) * e(5,g))*h**2              !M11                     4     5
     s(5,g) =(d(5) * e(4,g) + d(4) * e(5,g))*h**2              !M22                     5     4
     s(6,g) = d(6) * e(6,g) * h**2                             !M12                                 6
     s(7,g) = d(7) * e(7,g) * h                                !Q1                                       7
     s(8,g) = d(7) * e(8,g) * h                                !Q2                                            8
   CASE (3)           !orthotropic                                  E11   E22   E12   C11   C22   C12   G1    G2
     s(1,g) = d(1) * e(1,g) + d(2) * e(2,g)                    !N11   1     2
     s(2,g) = d(2) * e(1,g) + d(3) * e(2,g)                    !N22   2     3
     s(3,g) = d(4) * e(3,g)                                    !N12               4
     s(4,g) =(d(5) * e(4,g) + d(6) * e(5,g))*h**2              !M11                     5     6
     s(5,g) =(d(6) * e(4,g) + d(7) * e(5,g))*h**2              !M22                     6     7
     s(6,g) = d(8) * e(6,g) * h**2                             !M12                                 8
     s(7,g) = d(9) * e(7,g) * h                                !Q1                                       9
     s(8,g) =d(10) * e(8,g) * h                                !Q2                                           10
   CASE (4)           !anisotropic symmetric                        E11   E22   E12   C11   C22   C12   G1    G2
     s(1,g) =  d(1)*e(1,g)+ d(2)*e(2,g)+ d(3)*e(3,g)           !N11   1     2     3
     s(2,g) =  d(2)*e(1,g)+ d(4)*e(2,g)+ d(5)*e(3,g)           !N22   2     4     5
     s(3,g) =  d(3)*e(1,g)+ d(5)*e(2,g)+ d(6)*e(3,g)           !N12   3     5     6
     s(4,g) =( d(7)*e(4,g)+ d(8)*e(5,g)+ d(9)*e(6,g))*h**2     !M11                     7     8     9
     s(5,g) =( d(8)*e(4,g)+d(10)*e(5,g)+d(11)*e(6,g))*h**2     !M22                     8    10    11
     s(6,g) =( d(9)*e(4,g)+d(11)*e(5,g)+d(12)*e(6,g))*h**2     !M12                     9    11    12
     s(7,g) =(d(13)*e(7,g)+d(14)*e(8,g))*h                     !Q1                                      13   14
     s(8,g) =(d(14)*e(7,g)+d(15)*e(8,g))*h                     !Q2                                      14   15
   CASE (5)           !anisotropic non-symmetric                    E11   E22   E12   C11   C22   C12   G1    G2     E11   E22   E12   C11   C22   C12   G1   G2
     s(1,g) =  d(1)*e(1,g)+ d(2)*e(2,g)+ d(3)*e(3,g)        &  !N11   1     2     3                                                      1     2     3   10   11
            +  b(1)*e(4,g)+ b(2)*e(5,g)+ b(3)*e(6,g)        !& !N22   2     4     5                                                      4     5     6   12   13
            !+ b(10)*e(7,g)+b(11)*e(8,g)                       !N12   3     5     6                                +                     7     8     9   14   15
     s(2,g) =  d(2)*e(1,g)+ d(4)*e(2,g)+ d(5)*e(3,g)        &  !M11                     7     8     9                  1     4     7                     16   17
            +  b(4)*e(4,g)+ b(5)*e(5,g)+ b(6)*e(6,g)        !& !M22                     8    10    11                  2     5     8                     18   19
            !+ b(12)*e(7,g)+b(13)*e(8,g)                       !M12                     9    11    12                  3     6     9                     20   21
     s(3,g) =  d(3)*e(1,g)+ d(5)*e(2,g)+ d(6)*e(3,g)        &  !Q1                                      13   14       10    12    14    16    18    20
            +  b(7)*e(4,g)+ b(8)*e(5,g)+ b(9)*e(6,g)        !& !Q2                                      14   15       11    13    15    17    19    21
            !+ b(14)*e(7,g)+b(15)*e(8,g)
     s(4,g) =( d(7)*e(4,g)+ d(8)*e(5,g)+ d(9)*e(6,g))*h**2  &
            +  b(1)*e(1,g)+ b(4)*e(2,g)+ b(7)*e(3,g)        !&
            !+ b(16)*e(7,g)+b(17)*e(8,g)
     s(5,g) =( d(8)*e(4,g)+d(10)*e(5,g)+d(11)*e(6,g))*h**2  &
            +  b(2)*e(1,g)+ b(5)*e(2,g)+ b(8)*e(3,g)        !&
            !+ b(18)*e(7,g)+b(19)*e(8,g)
     s(6,g) =( d(9)*e(4,g)+d(11)*e(5,g)+d(12)*e(6,g))*h**2  &
            +  b(3)*e(1,g)+ b(6)*e(2,g)+ b(9)*e(3,g)        !&
            !+ b(20)*e(7,g)+b(21)*e(8,g)
     s(7,g) =(d(13)*sf(1)*e(7,g)+d(14)*sf(2)*e(8,g))*h      !&
            !+ b(11)*e(1,g)+b(13)*e(2,g)+b(15)*e(3,g)        &
            !+ b(16)*e(4,g)+b(18)*e(5,g)+b(20)*e(6,g)
     s(8,g) =(d(14)*sf(2)*e(7,g)+d(15)*sf(3)*e(8,g))*h      !&
            !+ b(10)*e(1,g)+b(12)*e(2,g)+b(14)*e(3,g)        &
            !+ b(17)*e(4,g)+b(19)*e(5,g)+b(21)*e(6,g)
   END SELECT
 END DO
 RETURN
 END SUBROUTINE istgp7
