 SUBROUTINE actu24(heade)
 ! updates internal variables PRESENT ==> LAST CONVERGED
 IMPLICIT NONE
 TYPE(ele24), POINTER :: heade

 INTEGER (kind=4) :: isec,nlayr,i,nvare,osec
 REAL(kind=8) :: defps
 LOGICAL :: newmt,found
 TYPE(ele24), POINTER :: e
 TYPE (section), POINTER :: sec    !pointer to a section data

 osec = -1
 e => heade
 DO
   IF( .NOT.ASSOCIATED(e) ) EXIT
   IF( ASSOCIATED(e%gausv) ) THEN   !Yield must be > zero
     isec = e%matno                   !associated material
     newmt = osec /= isec             !same material than previous ?
     IF(newmt)THEN                     !if new material, get properties
       CALL sect_search(isec,found,sec)
       osec = isec                    !keep present material
       nlayr = sec%iprop(1)           !number of layers
       nvare = sec%iprop(2)           !number of material components
     END IF
     DO i=1,nlayr
       defps = e%gausv(8,i)  !pointer to increment in plastic strain
       IF(defps > 0)THEN
         e%gausv(1:3,i) = e%gausv(5:7,i) !current into last converged
         e%gausv(4,i)   = e%gausv(4,i) + defps  !eff. pl. strain
       END IF
     END DO
   END IF
   e => e%next
 END DO

 RETURN
 END SUBROUTINE actu24
