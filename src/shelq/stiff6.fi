 SUBROUTINE stiff6(heade, coord, euler, gstif, force, nstre, zigzag)
!********************************************************************
!
!***   evaluation of tangent stiffness matrix for shell elements
!
!********************************************************************
 IMPLICIT NONE
!***  routine parameters
 INTEGER (kind=4), INTENT(IN) :: nstre
 REAL (kind=8), INTENT(IN) :: coord(:,:), euler(:,:)
 REAL (kind=8), INTENT(IN OUT) :: gstif(:),force(:)
 TYPE (ele06), POINTER :: heade
 LOGICAL, INTENT(IN) :: zigzag
!***  local variables
 TYPE (ele06), POINTER :: e
 LOGICAL ::  plast,newmt,dum
 INTEGER (kind=4) ielem,isec,np,matty,osec,nlayr,nvarl,ndofe,secty
 REAL (kind=8) ::  thick,sf(3)
 REAL (kind=8), ALLOCATABLE :: stiff(:),bmatx(:,:,:),bsmtx(:,:,:)
 TYPE(section), POINTER :: sec
 TYPE(mater), POINTER :: mat

!***   loop over all the elements

 osec = -1
 e => heade
 DO
   IF (.NOT.ASSOCIATED (e) ) EXIT    !last element processed => EXIT
   ielem = e%numel
   isec = e%matno
   newmt = osec /= isec
   IF(newmt)THEN                     !if new material, get properties
     sec => psecs(isec)%p
     secty = sec%secty
     matty = sec%secdef(3)
     IF( sec%mabas > 0 )THEN
       mat => sec%mtbas
       plast = matty <= 2 .AND. mat%matdef(3) > 1
       nlayr = sec%iprop(1)
     ELSE     !for sectyp = 15
       plast = .FALSE.
       nlayr = 1          !integrated
     END IF
     thick = sec%rprop(1)
     ndofe = 5
     IF( secty == 15 .AND. zigzag )THEN
       ndofe = 7
     END IF
     nvarl = nnode*ndofe
     IF( ALLOCATED(stiff)) DEALLOCATE(stiff,bmatx,bsmtx)
     ALLOCATE(stiff(nvarl*(nvarl+1)/2))
     IF(matty == 0 )THEN
       np = 12
       !CALL dmatx6(sec%rprop(6),sec%rprop(np),dum,dum,dum,dum,newmt)  !no effect in computations
     ELSE
       np = MIN(21,sec%secdef(2))
       IF(matty == 5)THEN
         IF(sec%rprop(4) /= 0d0 )THEN
           sf = sec%rprop(4)
         ELSE
           sf = sec%rprop(71:73)
         END IF
       END IF
     END IF
     ALLOCATE(bmatx(ndofe,nnode,nstre),bsmtx(ndofe,nnode,ngamm))
     bmatx = 0d0
     bsmtx = 0d0
     osec = isec
   END IF
   !  zero the elemental stiffness matrix
   stiff = 0d0

   CALL tanma6(nvarl,ndofe,ngaus,nstre,ngamm,nnode,nlayr,plast,matty,e%lnods,  &
               sec%rprop(np:),sec%rprop(6:),sf,coord,euler,posgp,              &
               e%dvolu,e%cartd,e%jacin,e%strsg,e%ambda,e%ehist,                &
               e%strap,e%stres,stiff,shape,bmatx,bsmtx,newmt,thick,            &
               e%nangl,e%jas)

   CALL stiaux(nnode,e%lnods,nvarl,ndofe,stiff(1),force(1),gstif(1) )
   e => e%next
 END DO
 RETURN

 END SUBROUTINE stiff6
